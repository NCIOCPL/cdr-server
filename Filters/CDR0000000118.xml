<?xml version="1.0" ?>
<!-- Filter title: Political SubUnit QC Report -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
                               version = "1.0">

 <xsl:output                    method = "html"/>

 <xsl:variable                    name = "docID"
                                select = "document('cdrutil:/docid')"/>

 <!--
 =========================================================================
 Main Template
 ========================================================================= -->
 <xsl:template                   match = "/">
  <HTML>
   <HEAD>
    <xsl:variable                 name = "cdrdocs"
                                select = "document('cdrx:/*/CdrCtl')"/>

    <TITLE>
     <xsl:value-of              select = "concat('CDR',
                                           number(
                                           substring-after($docID, 'CDR')))"/>
     <xsl:text>: </xsl:text>
     <xsl:value-of              select = "substring-before(
                                           concat($cdrdocs/CdrDocCtl/
                                                  DocTitle, ';'), ';')"/>
    </TITLE>
   </HEAD>
   <BASEFONT                      FACE = 'Arial, Helvetica, sans-serif' />
   <xsl:apply-templates         select = "PoliticalSubUnit"/>
  </HTML>
 </xsl:template>


 <!--
 ========================================================================
 Generate the HTML for the PoliticalSubUnit QC Report
 ======================================================================== -->
 <xsl:template                   match = "PoliticalSubUnit">
  <BODY>

  <!--
  ==================================================================
  Display Report heading and subheading
  ================================================================== -->
   <b>
    <font size='4'>
     <center>
      <xsl:text>Political SubUnit</xsl:text>
      <br/>
      <xsl:text>QC Report</xsl:text>
     </center>
    </font>
    <font size='3'>
     <center>
      <xsl:value-of             select = "translate(substring(
                                            document('cdrutil:/date'), 1, 16),
                                            'T', ' ')"/>
     </center>
    </font>
   </b>
   <p/>

   <b>
    <font size='4'>
    <xsl:value-of               select = "concat('CDR',
                                           number(
                                           substring-after($docID, 'CDR')))"/>
    </font>
   </b>
   <p/>

   <table  cellpadding='0' cellspacing='0' width='90%' border ='0'>
    <tr>
     <th align='left' width='40%' nowrap='1'>
      <xsl:text>Full Name:</xsl:text>
     </th>
     <td width='60%'>
      <xsl:value-of             select = "PoliticalSubUnitFullName"/>
     </td>
    </tr>
    <xsl:for-each               select = "PoliticalSubUnitShortName">
     <tr>
      <th align='left' width='40%' nowrap='1'>
       <xsl:if                    test = "not(preceding-sibling::
                                          PoliticalSubUnitShortName)">
        <xsl:text>Short Name:</xsl:text>
       </xsl:if>
      </th>
      <td>
       <xsl:value-of            select = "."/>
      </td>
     </tr>
    </xsl:for-each>
    <xsl:for-each               select = "PoliticalSubUnitAlternateName">
     <tr>
      <th align='left' width='40%' nowrap='1'>
       <xsl:if                    test = "not(preceding-sibling::
                                          PoliticalSubUnitAlternateName)">

        <xsl:text>Alternate Name:</xsl:text>
       </xsl:if>
      </th>
      <td>
       <xsl:value-of            select = "."/>
      </td>
     </tr>
    </xsl:for-each>
    <tr>
     <th align='left' width='40%'>
      <xsl:text>Country:</xsl:text>
     </th>
     <td>
      <xsl:value-of             select = "Country/CountryFullName"/>
      <xsl:text> (</xsl:text>
      <xsl:value-of             select = "concat('CDR', number(substring-after(
                                                  Country/@cdr:ref,'CDR')))"/>
      <xsl:text>)</xsl:text>
     </td>
    </tr>
    <xsl:for-each               select = "DateLastModified">
     <tr>
      <th align='left' nowrap='1'>
       <xsl:text>Date Last Modified:</xsl:text>
      </th>
      <td>
       <xsl:value-of            select = "."/>
      </td>
     </tr>
    </xsl:for-each>
   </table>
  </BODY>
 </xsl:template>

</xsl:transform>
