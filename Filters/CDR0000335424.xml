<CdrDoc Type='Filter' Id='CDR0000335424'>
<CdrDocCtl>
<DocValStatus readonly="yes">U</DocValStatus>
<DocActiveStatus readonly="yes">A</DocActiveStatus>
<DocTitle readonly="yes">Denormalization Filter: Summary</DocTitle>
<DocComment>CVS-V1.1: New Summary Denormalization (Bug 767)</DocComment><ReadyForReview readonly="yes">N</ReadyForReview>
</CdrDocCtl>
<CdrDocXml><![CDATA[<?xml      version = "1.0"?>
<!--
 ================================================================
     $Id$

     Filter to denormalize summary data.
        Written by Volker Englisch, 2003-08-12

     $Log: not supported by cvs2svn $
 ================================================================ -->
<xsl:transform                xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                                version = "1.0"
                              xmlns:cdr = "cips.nci.nih.gov/cdr">

 <xsl:output                     method = "xml" 
                   omit-xml-declaration = "no"/>

 <xsl:include href = "cdr:name:Module: Citation Denormalization"/>
 <xsl:include href = "cdr:name:Module: Multimedia Denormalization"/>
 <!-- 
 =================================================================
 Copy control information and generate current date
 ================================================================= -->
 <xsl:template                    match = "Summary">
  <xsl:copy>
   <xsl:apply-templates          select = "document('cdrutil:/docid')"/>
   <xsl:apply-templates          select = "document('cdrx:/*/CdrCtl')"/>

   <xsl:apply-templates/>
  </xsl:copy>
 </xsl:template>


 <!-- 
 =================================================================
 Test if a SummarySection is empty (no children, only white space)

 Such an element may appear if a complete SummarySection has been
 marked with the proposed insertion element and therefore all 
 content gets stripped but the SummarySection tags itself are left
 untouched.
 ================================================================= -->
 <xsl:template                   match = "SummarySection">
  <xsl:choose>
   <xsl:when                      test = "normalize-space(.)">
    <xsl:copy>
     <xsl:apply-templates       select = "node() | @*"/>
    </xsl:copy>
   </xsl:when>
   <xsl:otherwise>
    <!-- Do Nothing:  SummarySection is empty!  -->
   </xsl:otherwise>
  </xsl:choose>
 </xsl:template>


 <!-- 
 =================================================================
 Template to denormalize the Board element
 ================================================================= -->
 <xsl:template                    match = "Board">
  <xsl:variable                    name = "BoardID" 
                                 select = "@cdr:ref"/>
  <xsl:variable                    name = "BoardInfo" 
                                 select = "document(concat('cdr:',$BoardID,
                                                           '/last'))"/>
  <xsl:element                     name = "Board">
   <xsl:attribute                  name = "cdr:ref">
    <xsl:value-of                select = "@cdr:ref"/>
   </xsl:attribute>

   <xsl:value-of                 select = "$BoardInfo/Organization/
                                            OrganizationNameInformation/
                                            OfficialName/
                                            Name"/>
  </xsl:element>
 </xsl:template>


 <!-- 
 =================================================================
 Template to denormalize the BoardMember elements
 ================================================================= -->
 <xsl:template                    match = "BoardMember">
  <xsl:variable                    name = "BoardMemberID" 
                                 select = "@cdr:ref"/>
  <xsl:variable                    name = "BoardMemberInfo" 
                                 select = "document(concat('cdr:', 
                                                            $BoardMemberID,
                                                            '/last'))"/>
  <xsl:element                     name = "BoardMember">
   <xsl:attribute                  name = "cdr:ref">
    <xsl:value-of                select = "@cdr:ref"/>
   </xsl:attribute>

   <xsl:value-of                 select = "concat($BoardMemberInfo/
                                                   Person/
                                                   PersonNameInformation/
                                                   SurName,
                                                   ', ',
                                                  $BoardMemberInfo/
                                                   Person/
                                                   PersonNameInformation/
                                                   GivenName)"/>   
  </xsl:element>
 </xsl:template>


 <!-- 
 =================================================================
 Template to denormalize the Term elements
 ================================================================= -->
 <xsl:template                    match = "Term">
  <xsl:variable                    name = "TermID" 
                                 select = "@cdr:ref"/>
  <xsl:variable                    name = "TermInfo" 
                                 select = "document(concat('cdr:',$TermID,
                                                           '/lastp'))"/>
  <xsl:element                     name = "Term">
   <xsl:attribute                  name = "cdr:ref">
    <xsl:value-of                select = "@cdr:ref"/>
   </xsl:attribute>

   <xsl:value-of                 select = "$TermInfo/Term/
                                            PreferredName"/>
  </xsl:element>
 </xsl:template>


 <!-- 
 =================================================================
 Template to denormalize the Term elements
 ================================================================= -->
 <xsl:template                    match = "Diagnosis | Intervention | Drug">
  <xsl:variable                    name = "TermID" 
                                 select = "@cdr:ref"/>
  <xsl:variable                    name = "TermInfo" 
                                 select = "document(concat('cdr:',$TermID,
                                                           '/lastp'))"/>
  <xsl:element                     name = "{name()}">
   <xsl:attribute                  name = "cdr:ref">
    <xsl:value-of                select = "@cdr:ref"/>
   </xsl:attribute>

   <xsl:value-of                 select = "$TermInfo/Term/
                                            PreferredName"/>
  </xsl:element>
 </xsl:template>


 <!-- 
 =================================================================
 Template to denormalize the summary section intervention terms
 ================================================================= -->
<!--
 <xsl:template                   match = "Diagnosis    |
                                          Intervention |
                                          Drug">
  <xsl:variable                   name = "termID" 
                                select = "@cdr:ref"/>
  <xsl:variable                   name = "termDoc" 
                                select = "document(concat('cdr:',$termID,
                                                           '/lastp'))"/>
  <xsl:element                    name = "{name()}">
    <xsl:for-each               select = "@*">
     <xsl:attribute               name = "{name()}">
      <xsl:value-of             select = "."/>
     </xsl:attribute>
    </xsl:for-each>
    <xsl:apply-templates        select = "$termDoc/Term
                                           /PreferredName"/>
  </xsl:element>
 </xsl:template>
-->

 <!-- 
 =================================================================
 Template to denormalize the SummaryRef elements
 We need this denormalization in order to correctly create the 
 link in the QC report to the listed summaries.
 ================================================================= -->
 <xsl:template                    match = "SummaryRef |
                                           SummaryFragmentRef">
  <xsl:variable                    name = "SummaryRefID" 
                                 select = "substring-before(
                                             concat(@cdr:href, '#'), '#')"/>

  <xsl:variable                    name = "thisSummaryId"
                                 select = "document('cdrutil:/docid')"/>
  <xsl:variable                    name = "pubVersion"
                                 select = "document(concat('cdrutil:', 
                                                           '/get-pv-num/',
                                                       @cdr:href | @href))"/>

  <!--
  If a SummaryRef or SummaryFragmentRef is pointing to itself and the summary
  is new we can't expect a publishable version of the summary to exist.
  In this case we need to allow to denormalize with the last version.
  ====================================================================== -->
  <xsl:variable                   name = "SummaryRefInfo">
   <xsl:choose>
    <xsl:when                     test = "$pubVersion = 0
                                           and
                                           $SummaryRefID = $thisSummaryId">
     <xsl:copy-of              select = "document(concat('cdr:',
                                                           $SummaryRefID,
                                                           '/last'))"/>
    </xsl:when>
    <xsl:otherwise>
     <xsl:copy-of              select = "document(concat('cdr:',
                                                           $SummaryRefID,
                                                           '/lastp'))"/>
    </xsl:otherwise>
   </xsl:choose>
  </xsl:variable>

  <xsl:element                     name = "{name()}">
   <xsl:attribute                  name = "cdr:href">
    <xsl:value-of                select = "@cdr:href"/>
   </xsl:attribute>

   <SummaryRefText>
    <xsl:value-of                select = "."/>
   </SummaryRefText>

   <LinkedSummary>
    <LinkedSummaryType>
     <xsl:value-of               select = "$SummaryRefInfo/Summary
                                             /SummaryMetaData
                                             /SummaryType"/>
    </LinkedSummaryType>
    <LinkedSummaryAudience>
     <xsl:value-of               select = "$SummaryRefInfo/Summary
                                             /SummaryMetaData
                                             /SummaryAudience"/>
    </LinkedSummaryAudience>

    <xsl:if                        test = "$SummaryRefInfo/Summary
                                             //KeyPoint">
     <LinkedKeyPoints>
      <xsl:value-of              select = "count($SummaryRefInfo/Summary
                                             //KeyPoint)"/>
     </LinkedKeyPoints>
    </xsl:if>
   </LinkedSummary>
  </xsl:element>
 </xsl:template>


 <!-- 
 =================================================================
 Template to denormalize the Term elements
 ================================================================= -->
 <xsl:template                    match = "MiscellaneousDocLink">
  <xsl:variable                    name = "MiscID" 
                                 select = "@cdr:ref"/>
  <xsl:variable                    name = "MiscInfo" 
                                 select = "document(concat('cdr:',$MiscID,
                                                           '/lastp'))"/>
  <xsl:variable                    name = "MiscIDFrag"
                                 select = "substring-after($MiscID, '#')"/>
  <xsl:element                     name = "MiscellaneousDocLink">
   <xsl:attribute                  name = "cdr:ref">
    <xsl:value-of                select = "@cdr:ref"/>
   </xsl:attribute>

   <xsl:copy-of                  select = "$MiscInfo/
                                            MiscellaneousDocument/
                                            MiscellaneousDocumentText//
                                            Section[@cdr:id = $MiscIDFrag]"/>
  </xsl:element>
 </xsl:template>


 <!-- 
 ==================================================================
 Template rule used to do XML to XML transformations which
 copies any attribute node, or node that is child of of something 
 ================================================================== -->
 <xsl:template                    match = "@*|node()">
  <xsl:copy>
   <xsl:apply-templates          select = "@*|node()"/>
  </xsl:copy> 
 </xsl:template>

</xsl:transform>
]]>
</CdrDocXml>
</CdrDoc>