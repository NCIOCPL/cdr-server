<?xml version="1.0" ?>
<!-- Filter title: Citation QC Report -->
<!--
==============================================================
Filter to create a Citation QC Report
============================================================== -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr">

 <xsl:output                    method = "html"/>

 <!-- Defining standard HTML Attribute Sets -->
 <xsl:attribute-set               name = "table">
  <xsl:attribute                  name = "border">0</xsl:attribute>
  <xsl:attribute                  name = "width">100%</xsl:attribute>
  <xsl:attribute                  name = "cellspacing">0</xsl:attribute>
  <xsl:attribute                  name = "cellpadding">0</xsl:attribute>
 </xsl:attribute-set>

 <xsl:attribute-set               name = "cell1of2">
  <xsl:attribute                  name = "width">35%</xsl:attribute>
  <xsl:attribute                  name = "valign">top</xsl:attribute>
 </xsl:attribute-set>

 <xsl:attribute-set               name = "cell2of2">
  <xsl:attribute                  name = "width">65%</xsl:attribute>
  <xsl:attribute                  name = "valign">top</xsl:attribute>
 </xsl:attribute-set>

 <xsl:variable                    name = "docID"
                                select = "document('cdrutil:/docid')"/>
 <!--
 ==================================================================
 Create HTML Output
   Updated on February 13, 2002  by C. Burg
   Updated on July 16, 2002 to include formatted reference
   <!DOCTYPE HTML PUBLIC '-//IETF//DTD HTML//EN' />
 ================================================================== -->
 <xsl:template                   match = "/">
  <HTML>
   <HEAD>
    <xsl:variable                 name = "cdrdocs"
                                select = "document('cdrx:/*/CdrCtl')"/>
    <TITLE>
     <xsl:value-of              select = "concat('CDR',
                                           number(
                                           substring-after($docID, 'CDR')))"/>
     <xsl:text>: </xsl:text>
     <xsl:value-of              select = "substring-before(
                                           concat($cdrdocs/CdrDocCtl/
                                                  DocTitle, ';'), ';')"/>
    </TITLE>
   </HEAD>
   <BASEFONT                      FACE = 'Arial, Helvetica, sans-serif'/>
   <xsl:apply-templates         select = "Citation"/>
  </HTML>
 </xsl:template>


 <!--
 =====================================================================
 Template to display Citation Report
 ===================================================================== -->
 <xsl:template                   match = "Citation">
  <BODY>
   <!--
   ==============================================================
   Display Report heading and subheading
   ============================================================== -->
   <b>
    <font size="4">
     <center>
      <xsl:text>Citations</xsl:text>
      <br/>
      <xsl:text>QC Report</xsl:text>
     </center>
    </font>
    <font size='3'>
     <center>
      <xsl:value-of             select = "translate(substring(
                                            document('cdrutil:/date'), 1, 16),
                                            'T', ' ')"/>
     </center>
    </font>
   </b>
   <p/>

   <b>
    <font size='4'>
     <xsl:value-of              select = "concat('CDR',
                                           number(
                                           substring-after($docID, 'CDR')))"/>
    </font>
   </b>
   <p/>

   <!--
   ======================================================================
   Generate CitationType, CitationTitle,Authors, Publication details,
   and CancerLit ID for PDQ citations.
   ====================================================================== -->
   <xsl:apply-templates         select = "PDQCitation"/>
   <xsl:apply-templates         select = "PubmedArticle"/>

   <!--
   ===================================================================
   Display verification details,DatedAction, and Date last modified,
   and PDQ Key
   =================================================================== -->
   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Verified</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "VerificationDetails/Verified"/>
     </td>
    </tr>
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Verified In</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "VerificationDetails/VerifiedIn"/>
     </td>
    </tr>
   </table>
   <p/>

   <b>
    <font size="4">
     <xsl:text>Dated Action(s)</xsl:text>
    </font>
   </b>
   <xsl:apply-templates         select = "DatedAction"/>

   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
       <b>Date Last Modified</b>
      </td>
      <td       xsl:use-attribute-sets = "cell2of2">
       <xsl:value-of            select = "DateLastModified"/>
      </td>
     </tr>
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>PdqKey</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "PdqKey"/>
     </td>
    </tr>
   </table>

   <xsl:if                        test = "PubmedArticle">
   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
       <b>Medline ID</b>
      </td>
      <td       xsl:use-attribute-sets = "cell2of2">
       <xsl:value-of            select = "PubmedArticle/MedlineCitation/
                                          MedlineID"/>
      </td>
     </tr>
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>PMID</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:element                name = "a">
       <xsl:attribute             name = "href">
        <xsl:value-of           select = "concat('https://www.ncbi.nlm.nih.gov',
                                                 '/entrez/query.fcgi?',
                                                 'cmd=Retrieve',
                                                 '&amp;db=pubmed',
                                                 '&amp;dopt=Abstract',
                                                 '&amp;list_uids=',
                                                 PubmedArticle/MedlineCitation/
                                                 PMID)"/>
       </xsl:attribute>
       <xsl:attribute             name = "target">
        <xsl:text>new</xsl:text>
       </xsl:attribute>

       <xsl:value-of            select = "PubmedArticle/MedlineCitation/
                                          PMID"/>
      </xsl:element>
     </td>
    </tr>
   </table>
   </xsl:if>

   <p/><hr/>
   <b>
    <font size="4">
     <xsl:text>Formatted Reference</xsl:text>
    </font>
   </b>
   <br/>
   <xsl:value-of                select = "FormattedReference"/>
   <br/>
  </BODY>
 </xsl:template>


 <!--
 ======================================================================
 Template for PDQCitations
 ====================================================================== -->
 <xsl:template                   match = "PDQCitation">
  <b>
   <font size="4">
    <xsl:text>PDQ Citation</xsl:text>
   </font>
  </b>

  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Citation Type</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "CitationType"/>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Title</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "CitationTitle"/>
    </td>
   </tr>
  </table>
  <p/>

  <b>
   <font size="4">
    <xsl:text>Authors</xsl:text>
   </font>
  </b>
  <br/>
  <b>
   <xsl:text>Personal Name</xsl:text>
  </b>
  <br />

  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Complete Author List</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "AuthorList/@Complete"/>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Editors</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "AuthorList/@Editors"/>
    </td>
   </tr>
  </table>

  <xsl:apply-templates          select = "AuthorList/Author"/>
  <xsl:apply-templates          select = "PublicationDetails"/>
  <xsl:apply-templates          select = "CancerLitAbstract"/>
 </xsl:template>


 <!--
 ======================================================================
 Template to display the authors
 ====================================================================== -->
 <xsl:template                   match = "AuthorList/Author">
  <table        xsl:use-attribute-sets = "table">
  <xsl:if                         test = "LastName">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Last Name</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "LastName"/>
    </td>
   </tr>
  </xsl:if>
  <xsl:if                         test = "FirstName | ForeName">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>First Name</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "FirstName | ForeName"/>
    </td>
   </tr>
  </xsl:if>
  <xsl:if                         test = "Initials">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Initials/Middle Name</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "Initials"/>
    </td>
   </tr>
  </xsl:if>
  <xsl:if                         test = "Suffix">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Suffix</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "Suffix"/>
    </td>
   </tr>
  </xsl:if>
  <xsl:if                         test = "CollectiveName">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Collective Name</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "CollectiveName"/>
    </td>
   </tr>
  </xsl:if>
  </table>
  <br/>
 </xsl:template>


 <!--
 ====================================================================
 Template to display the PublicationDetails
 ==================================================================== -->
 <xsl:template                   match = "PublicationDetails">
  <b>
   <font size="4">
    <xsl:text>Publication Details</xsl:text>
   </font>
  </b>
  <br />

  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Published In</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:if                      test = "PublishedIn">
      <xsl:variable               name = "cdrId"
                                select = "concat('CDR',
                                           number(
                                            substring-after(
                                             substring-before(
                                              concat(PublishedIn/
                                               @cdr:ref, '#'), '#'), 'CDR')))"/>
      <xsl:value-of             select = "PublishedIn/Citation/
                                          PDQCitation/CitationTitle"/>
      <xsl:text> (</xsl:text>

      <xsl:element                name = "a">
       <xsl:attribute             name = "href">
        <xsl:value-of           select = "concat('/cgi-bin/cdr/Filter.py',
                                                 '?DocId=',$cdrId,'&amp;',
                                                 'DocVer=lastp&amp;',
                                                 'Filter=set:',
                                                 'QC%20Citation%20Set')"/>
       </xsl:attribute>
       <xsl:attribute             name = "target">
        <xsl:value-of           select = "'new'"/>
       </xsl:attribute>
       <xsl:value-of            select = "$cdrId"/>
      </xsl:element>

      <xsl:text>)</xsl:text>
     </xsl:if>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Publication Information</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "OtherPublicationInformation/
                                          PublicationInformation"/>
    </td>
   </tr>
   <xsl:if                        test = "OtherPublicationInformation/
                                          InternetInformation">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Internet Information</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "OtherPublicationInformation/
                                          InternetInformation"/>
     <br/>
     <xsl:element                 name = "a">
      <xsl:attribute              name = "href">
       <xsl:choose>
        <xsl:when                 test = "contains(
                                          OtherPublicationInformation/
                                          InternetInformation/
                                          ExternalRef/@cdr:xref,'http')">
         <xsl:value-of          select = "OtherPublicationInformation/
                                          InternetInformation/
                                          ExternalRef/@cdr:xref"/>
        </xsl:when>
        <xsl:otherwise>
         <xsl:value-of          select = "concat('http://',
                                          OtherPublicationInformation/
                                          InternetInformation/
                                          ExternalRef/@cdr:xref)"/>
        </xsl:otherwise>
       </xsl:choose>
      </xsl:attribute>
      <xsl:attribute              name = "target">
       <xsl:text>new</xsl:text>
      </xsl:attribute>
      <xsl:value-of             select = "OtherPublicationInformation/
                                          InternetInformation/
                                          ExternalRef/@cdr:xref"/>
     </xsl:element>
    </td>
   </tr>
   </xsl:if>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Publication Year</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "PublicationYear"/>
    </td>
   </tr>
  </table>
  <p/>
 </xsl:template>


 <!--
 ==========================================================================
 ========================================================================== -->
 <xsl:template                   match = "CancerLitAbstract">
  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>CancerLit Abstract</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "AbstractText"/>
    </td>
   </tr>
  </table>
  <p/>
 </xsl:template>


 <!--
 ====================================================================
 Template to display the PublicationDetails
 ==================================================================== -->
 <xsl:template                    name = "pubmedDetails">
  <b>
   <font size="4">
    <xsl:text>Publication Details</xsl:text>
   </font>
  </b>
  <br />

  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Published In</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "MedlineJournalInfo/MedlineTA"/>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Other Publication Information</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:apply-templates       select = "Article/Journal/JournalIssue"/>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Publication Year</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:choose>
      <xsl:when                   test = "Article/Journal/
                                          JournalIssue/PubDate/
                                          MedlineDate">
       <xsl:value-of            select = "Article/Journal/
                                          JournalIssue/PubDate/
                                          MedlineDate"/>
      </xsl:when>
      <xsl:otherwise>
       <xsl:value-of            select = "Article/Journal/
                                          JournalIssue/PubDate/
                                          Year"/>
      </xsl:otherwise>
     </xsl:choose>
    </td>
   </tr>
  </table>
  <p/>
 </xsl:template>


 <!--
 ==========================================================================
 ========================================================================== -->
 <xsl:template                   match = "DatedAction">
  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Action Description</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "ActionDescription"/>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Action Date</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "ActionDate"/>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Comment</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "Comment"/>
    </td>
   </tr>
  </table>
  <p/>
 </xsl:template>


 <!--
 ======================================================================
 Template for PubmedArticle
 ====================================================================== -->
 <xsl:template                   match = "PubmedArticle">
  <b>
   <font size="4">
    <xsl:text>PubMed Article</xsl:text>
   </font>
  </b>
  <br/>

  <xsl:apply-templates          select = "MedlineCitation"/>
 </xsl:template>


 <!--
 ======================================================================
 ====================================================================== -->
 <xsl:template                   match = "MedlineCitation">
  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Title</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "Article/ArticleTitle"/>
    </td>
   </tr>
  </table>
  <p/>

  <b>
   <font size="4">
    <xsl:text>Authors</xsl:text>
   </font>
  </b>
  <br/>

  <xsl:apply-templates          select = "Article/AuthorList/Author"/>
  <xsl:call-template              name = "pubmedDetails"/>
 </xsl:template>


 <!--
 ====================================================================
 Template to create JournalIssue
 ==================================================================== -->
 <xsl:template                   match = "JournalIssue">
  <xsl:choose>
   <!-- Citation contains Volume and Issue information -->
   <xsl:when                      test = "Issue">
    <xsl:value-of               select = "concat(Volume,' ',
                                                 '(',Issue,')',': ')"/>
   </xsl:when>
   <xsl:otherwise>
    <xsl:choose>
     <!-- Citation contains Supplement information in the Volume element -->
     <xsl:when                    test = "contains(Volume, 'Suppl')">
      <xsl:value-of             select = "substring-before(Volume, ' ')"/>
      <xsl:text> (</xsl:text>
      <xsl:value-of             select = "substring-after(Volume, ' ')"/>
      <xsl:text>)</xsl:text>
     </xsl:when>
     <xsl:otherwise>
      <xsl:value-of             select = "Volume"/>
     </xsl:otherwise>
    </xsl:choose>

    <xsl:text>: </xsl:text>
   </xsl:otherwise>
  </xsl:choose>

  <xsl:value-of                 select = "../../Pagination/MedlinePgn"/>
  <xsl:text>, </xsl:text>

  <xsl:if                         test = "PubDate/MedlineDate">
   <xsl:value-of                select = "PubDate/MedlineDate"/>
  </xsl:if>
  <xsl:if                         test = "PubDate/Year">
   <xsl:value-of                select = "PubDate/Year"/>
  </xsl:if>
 </xsl:template>
</xsl:transform>
