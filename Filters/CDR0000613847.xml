<?xml version="1.0" ?>
<!-- Filter title: Glossary Term Name QC Report filter -->
<!--
================================================================

Filter to display Glossary Term Name QC Reports
Volker Englisch         2007-09-26

BZIssue::5166 - QC reports showing in table format

================================================================ -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr"
                               version = "1.0">

 <xsl:output                    method = "html"
                  omit-xml-declaration = "no"
                              encoding = "utf-8"/>

 <xsl:include   href = "cdr:name:Module: Attribute Sets"/>
 <xsl:include   href = "cdr:name:Module: InLine Markup Formatter"/>
 <xsl:include   href = "cdr:name:Module: STYLE Default"/>
 <xsl:include   href = "cdr:name:Module: STYLE RS Insertion Deletion"/>

 <xsl:variable                    name = "docID"
                                select = "document('cdrutil:/docid')"/>
 <xsl:variable                    name = "intID"
                                select = "concat('',
                                           number(
                                           substring-after($docID, 'CDR')))"/>
 <xsl:variable                    name = "whoAmI"
                                select = "'GlossaryTermName'"/>
 <xsl:variable                    name = "sqlQuery">
     SELECT active_status
       FROM document
      WHERE id = ?
 </xsl:variable>

 <!--
 Note: Don't perform a normalize-space here since it would convert
       the resulting node into a string
 ================================================================== -->
 <xsl:variable                    name = "queryRows"
                                select = "document(cdr:escape-uri(
                                           concat('cdrutil:/sql-query/',
                                                   $sqlQuery,
                                                   '~',
                                                   $intID)))"/>
 <xsl:variable                    name = "isBlocked">
  <xsl:value-of                 select = "$queryRows
                                          /SqlResult
                                          /row/col"/>
 </xsl:variable>



 <!--
 ====================================================================
 This program is a filter to create a Glossary Term Name QC Report
 ==================================================================== -->
 <xsl:template                   match = "/">
  <!-- <!DOCTYPE HTML PUBLIC '-//IETF//DTD HTML//EN' /> -->
  <HTML>
   <HEAD>
    <xsl:variable                 name = "cdrdocs"
                                select = "document('cdrx:/*/CdrCtl')"/>
    <TITLE>
     <xsl:value-of              select = "concat('CDR',
                                           number(
                                           substring-after($docID, 'CDR')))"/>
     <xsl:text>: </xsl:text>
     <xsl:value-of              select = "substring-before(
                                           concat($cdrdocs/CdrDocCtl/
                                                  DocTitle, ';'), ';')"/>
    </TITLE>

    <STYLE type='text/css'>
     <xsl:call-template           name = "defaultStyle"/>
     <xsl:call-template           name = "rsInsertionDeletionStyle"/>

     .attribute  { font-weight: normal;
                   font-style: italic;
                   background-color: #FFFFFF; }
     .big        { font-size: 14pt;
                   font-weight: bold; }
     .bold       { font-weight: bold; }
     .name       { color: blue;
                   font-weight: bold;
                   background-color: #EEEEEE; }
     .blocked    { color: red; }
    </STYLE>
   </HEAD>
   <BASEFONT                      FACE = 'Arial, Helvetica, sans-serif' />
   <xsl:apply-templates         select = "GlossaryTermName"/>
  </HTML>
 </xsl:template>


 <!--
 ==============================================================
 Generate the HTML for the glossary term QC Report
 ============================================================== -->
 <xsl:template                   match = "GlossaryTermName">
  <BODY>
  <!--
  =============================================================
  Display Report heading and subheading
  ============================================================= -->
  <span class="big">
   <center>
    <xsl:text>Glossary Term Name</xsl:text>
    <br/>
    <xsl:text>QC Report</xsl:text>
   </center>
  </span>
  <b>
   <center>
     <xsl:value-of              select = "translate(substring(
                                          document('cdrutil:/date'), 1, 16),
                                          'T', ' ')"/>
   </center>
  </b>
  <p/>


  <!--
  ====================================================================
  Display CDR ID
  ==================================================================== -->
  <span class="big">
   <xsl:choose>
    <xsl:when                     test = "not($isBlocked = 'I')">
     <xsl:value-of              select = "concat('CDR',
                                          number(
                                          substring-after($docID, 'CDR')))"/>
    </xsl:when>
    <xsl:otherwise>
     <span class="blocked">
      <xsl:value-of             select = "concat('CDR',
                                          number(
                                          substring-after($docID, 'CDR')))"/>

      <xsl:text> [BLOCKED]</xsl:text>
     </span>
    </xsl:otherwise>
   </xsl:choose>
  </span>
  <p/>

  <!--
  ===============================================================
  Display Name Information
  =============================================================== -->
  <xsl:apply-templates          select = "TermName"/>

  <br/>

  <table        xsl:use-attribute-sets = "table">
   <xsl:if                        test = "TermNameStatus">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Status</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "TermNameStatus"/>
     </td>
    </tr>
   </xsl:if>
   <xsl:if                        test = "TermNameStatusDate">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Status Date</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "TermNameStatusDate"/>
     </td>
    </tr>
   </xsl:if>
  </table>
  <br/>

  <!--
  ===============================================================
  Display Translated Information
  =============================================================== -->
  <xsl:apply-templates          select = "TranslatedName"/>

  <br/>

  <xsl:apply-templates          select = "ProcessingStatuses"/>

  <!--
  ===============================================================
  Display Source Information
  =============================================================== -->
  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Glossary Term Concept</b>
    </td>
    <td class="qc_td">
     <xsl:choose>
      <xsl:when                   test = "string-length(
                                            GlossaryTermConcept/
                                            TermDefinition/
                                            DefinitionText) &lt; 50">
       <xsl:value-of            select = "GlossaryTermConcept/
                                            TermDefinition/
                                            DefinitionText"/>
      </xsl:when>
      <xsl:otherwise>
       <xsl:value-of            select = "concat(substring(
                                            GlossaryTermConcept/
                                            TermDefinition/
                                            DefinitionText, 1, 46), ' ...')"/>
      </xsl:otherwise>
     </xsl:choose>

     <xsl:variable                name = "cdrId"
                                select = "concat('CDR',
                                           number(
                                            substring-after(
                                             substring-before(
                                              concat(
                                                 GlossaryTermConcept/@cdr:ref,
                                                        '#'), '#'), 'CDR')))"/>
     <xsl:text> (</xsl:text>
     <xsl:element                 name = "a">
      <xsl:attribute              name = "href">
       <xsl:value-of            select = "concat('/cgi-bin/cdr/QcReport.py',
                                                 '?Session=guest',
                                                 '&amp;DocVersion=-1',
                                                 '&amp;DocId=',
                                                 $cdrId)"/>
      </xsl:attribute>
      <xsl:attribute              name = "target">
       <xsl:text>new</xsl:text>
      </xsl:attribute>
      <xsl:value-of             select = "$cdrId"/>
     </xsl:element>

     <xsl:text>)</xsl:text>
    </td>
   </tr>
  </table>

  <p/>


  <!--
  ===============================================================
  Display the ReplacementText elements
  =============================================================== -->
  <xsl:if                         test = "ReplacementText">
   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Replacement Text</b>
     </td>
     <td class="qc_td">  </td>
    </tr>
    <xsl:apply-templates        select = "ReplacementText"/>
   </table>
  </xsl:if>

  <xsl:if                         test = "Comment">
   <table       xsl:use-attribute-sets = "table">
    <xsl:apply-templates        select = "Comment"
                                  mode = "withAttribute">
     <xsl:with-param              name = "label"
                                select = "'Comment'"/>
    </xsl:apply-templates>
   </table>
  </xsl:if>

  </BODY>
 </xsl:template>


 <!--
 ==================================================================
 Template to display individual TermDefinitions
 ================================================================== -->
 <xsl:template                   match = "TermName">
  <table        xsl:use-attribute-sets = "table">
   <tr class="name">
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Name</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:apply-templates       select = "TermNameString"/>

    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Language</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:text>en</xsl:text>
    </td>
   </tr>

   <xsl:if                        test = "TermPronunciation">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Term Pronunciation</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:apply-templates      select = "TermPronunciation"/>
     </td>
    </tr>
   </xsl:if>
   <xsl:if                        test = "PronunciationResource">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Pronunciation Resource</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:for-each             select = "PronunciationResource">
       <li>
        <xsl:value-of            select = "."/>
       </li>
      </xsl:for-each>
     </td>
    </tr>
   </xsl:if>
  </table>
  <table        xsl:use-attribute-sets = "table">
   <xsl:if                        test = "TermNameSource">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Term Name Source</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "TermNameSource"/>
     </td>
    </tr>
   </xsl:if>
   <xsl:if                        test = "TermNameVerificationResource">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Term Name Verification Resource</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "TermNameVerificationResource"/>
     </td>
    </tr>
   </xsl:if>
   <xsl:if                        test = "Comment">
    <xsl:apply-templates        select = "Comment"
                                  mode = "withAttribute">
     <xsl:with-param              name = "label"
                                select = "'Comment'"/>
    </xsl:apply-templates>
   </xsl:if>
   <xsl:if                        test = "DateLastModified">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Date Last Modified</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "DateLastModified"/>
     </td>
    </tr>
   </xsl:if>
  </table>
 </xsl:template>


 <!--
 ==================================================================
 Template to display the translated Term names
 ================================================================== -->
 <xsl:template                   match = "TranslatedName">
  <table        xsl:use-attribute-sets = "table">
   <tr class="name">
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Name</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:apply-templates       select = "TermNameString"/>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Language</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:apply-templates       select = "@language"/>
    </td>
   </tr>

   <xsl:if                        test = "TranslationResource">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Translation Resource</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:for-each             select = "TranslationResource">
       <li>
        <xsl:value-of            select = "."/>
       </li>
      </xsl:for-each>
     </td>
    </tr>
   </xsl:if>
  </table>
  <table        xsl:use-attribute-sets = "table">
   <xsl:if                        test = "TranslatedNameStatus">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Translated Name Status</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "TranslatedNameStatus"/>
     </td>
    </tr>
   </xsl:if>
   <xsl:if                        test = "TranslatedNameStatusDate">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Translated Name Status Date</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "TranslatedNameStatusDate"/>
     </td>
    </tr>
   </xsl:if>
   <xsl:if                        test = "Comment">
    <xsl:apply-templates        select = "Comment"
                                  mode = "withAttribute">
     <xsl:with-param              name = "label"
                                select = "'Comment'"/>
    </xsl:apply-templates>
   </xsl:if>
   <xsl:if                        test = "DateLastModified">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Date Last Modified</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "DateLastModified"/>
     </td>
    </tr>
   </xsl:if>
  </table>
  <br/>
 </xsl:template>


 <!--
 ==================================================================
 Template to display the Processing Statuses Block
 ================================================================== -->
 <xsl:template                   match = "ProcessingStatuses">
  <span class="big">Processing Statuses</span>
  <br/>

  <xsl:apply-templates          select = "ProcessingStatus"/>
 </xsl:template>


 <!--
 ==================================================================
 Template to display the Processing Status Block
 ================================================================== -->
 <xsl:template                   match = "ProcessingStatus">
  <span class="bold">Processing Status</span>

  <table        xsl:use-attribute-sets = "table">
   <xsl:for-each                select = "ProcessingStatusValue">
    <xsl:call-template            name = "rowWithAttributes">
     <xsl:with-param              name = "label"
                                select = "'Status'"/>
     <xsl:with-param              name = "indent"
                                select = "'Y'"/>
    </xsl:call-template>
   </xsl:for-each>

   <xsl:if                        test = "Comment">
    <xsl:apply-templates        select = "Comment"
                                  mode = "withAttribute">
     <xsl:with-param              name = "label"
                                select = "'Comment'"/>
     <xsl:with-param              name = "indent"
                                select = "'Y'"/>
    </xsl:apply-templates>
   </xsl:if>


  </table>
  <br/>
 </xsl:template>


 <!--

 =======================================================================
 Template for Admin Information Comments
 ======================================================================= -->
 <xsl:template                   match = "Comment"
                                  mode = "withAttribute">
  <xsl:param                      name = "label"
                                select = "'Comment'"/>
  <xsl:param                      name = "indent"
                                select = "'N'"/>

 <xsl:choose>
  <xsl:when                       test = "$indent = 'N'">
  <tr>
   <td          xsl:use-attribute-sets = "cell1of2">
    <b>
     <xsl:value-of              select = "$label"/>
    </b>
   </td>
   <td          xsl:use-attribute-sets = "cell2of2">
    <span class="attribute">
     <xsl:if                      test = "@audience">
      <xsl:text>audience=</xsl:text>
      <xsl:value-of             select = "@audience"/>
     </xsl:if>
     <xsl:if                      test = "@date">
      <xsl:text> date=</xsl:text>
      <xsl:value-of             select = "@date"/>
     </xsl:if>
     <xsl:if                      test = "@user">
      <xsl:text> user=</xsl:text>
      <xsl:value-of             select = "@user"/>
     </xsl:if>
    </span>
   </td>
  </tr>
  <tr>
   <td class="qc_td"><xsl:text> </xsl:text></td>
   <td class="qc_td">
    <xsl:value-of              select = "."/>
    <p/>
   </td>
  </tr>
  </xsl:when>
  <xsl:otherwise>
  <tr>
   <td          xsl:use-attribute-sets = "cell1iof2"> </td>
   <td          xsl:use-attribute-sets = "cell2iof2">
    <b>
     <xsl:value-of              select = "$label"/>
    </b>
   </td>
   <td          xsl:use-attribute-sets = "cell2of2">
    <span class="attribute">
     <xsl:if                      test = "@audience">
      <xsl:text>audience=</xsl:text>
      <xsl:value-of             select = "@audience"/>
     </xsl:if>
     <xsl:if                      test = "@date">
      <xsl:text> date=</xsl:text>
      <xsl:value-of             select = "@date"/>
     </xsl:if>
     <xsl:if                      test = "@user">
      <xsl:text> user=</xsl:text>
      <xsl:value-of             select = "@user"/>
     </xsl:if>
    </span>
   </td>
  </tr>
  <tr>
   <td          xsl:use-attribute-sets = "cell1iof2"> </td>
   <td          xsl:use-attribute-sets = "cell2iof2"> </td>
   <td class="qc_td">
    <xsl:value-of              select = "."/>
    <p/>
   </td>
  </tr>
  </xsl:otherwise>
  </xsl:choose>
 </xsl:template>

 <!--
 =======================================================================
 Template for Admin Information Comments
 ======================================================================= -->
 <xsl:template                    name = "rowWithAttributes"
                                  mode = "withAttribute">
  <xsl:param                      name = "label"
                                select = "'Status'"/>
  <xsl:param                      name = "indent"
                                select = "'N'"/>

 <xsl:choose>
  <xsl:when                       test = "$indent = 'N'">
  <tr>
   <td          xsl:use-attribute-sets = "cell1of2">
    <b>
     <xsl:value-of              select = "$label"/>
    </b>
   </td>
   <td          xsl:use-attribute-sets = "cell2of2">
    <span class="attribute">
     <xsl:if                      test = "@audience">
      <xsl:text>audience=</xsl:text>
      <xsl:value-of             select = "@audience"/>
     </xsl:if>
     <xsl:if                      test = "@date">
      <xsl:text> date=</xsl:text>
      <xsl:value-of             select = "@date"/>
     </xsl:if>
     <xsl:if                      test = "@user">
      <xsl:text> user=</xsl:text>
      <xsl:value-of             select = "@user"/>
     </xsl:if>
    </span>
   </td>
  </tr>
  <tr>
   <td class="qc_td"><xsl:text> </xsl:text></td>
   <td class="qc_td">
    <xsl:value-of              select = "."/>
    <p/>
   </td>
  </tr>
  </xsl:when>
  <xsl:otherwise>
  <tr>
   <td          xsl:use-attribute-sets = "cell1iof2"> </td>
   <td          xsl:use-attribute-sets = "cell2iof2">
    <b>
     <xsl:value-of              select = "$label"/>
    </b>
   </td>
   <td          xsl:use-attribute-sets = "cell2of2">
    <span class="attribute">
     <xsl:if                      test = "@audience">
      <xsl:text>audience=</xsl:text>
      <xsl:value-of             select = "@audience"/>
     </xsl:if>
     <xsl:if                      test = "@date">
      <xsl:text> date=</xsl:text>
      <xsl:value-of             select = "@date"/>
     </xsl:if>
     <xsl:if                      test = "@user">
      <xsl:text> user=</xsl:text>
      <xsl:value-of             select = "@user"/>
     </xsl:if>
    </span>
   </td>
  </tr>
  <tr>
   <td class="qc_td"><xsl:text> </xsl:text></td>
   <td class="qc_td"><xsl:text> </xsl:text></td>
   <td class="qc_td">
    <xsl:value-of              select = "."/>
    <p/>
   </td>
  </tr>
  </xsl:otherwise>
  </xsl:choose>
 </xsl:template>


 <!--
 ======================================================================
 Template to display the Replacement text and PlaceHolders
 ====================================================================== -->
 <xsl:template                  match = "ReplacementText">
  <tr>
   <td class="qc_td">
    <xsl:value-of              select = "@name"/>
   </td>
   <td class="qc_td">
    <xsl:value-of              select = "."/>
   </td>
  </tr>
 </xsl:template>

 <!--

 We need to display the Glossary Term Name/Concept report with
 insertion/deletion markup.
 =================================================================== -->
 <xsl:template                   match = "Insertion">
  <span class="insertapproved">
   <xsl:apply-templates/>
  </span>
 </xsl:template>

 <xsl:template                   match = "Deletion">
  <span class="deleteapproved">
   <xsl:apply-templates/>
  </span>
 </xsl:template>
</xsl:transform>
