<?xml version="1.0" ?>
<!-- Filter title: Global Change: Organization Link -->

<!--myTitle>  Creator:  Volker Englisch
              Created:  06/20/2002</myTitle-->

<xsl:transform              xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                            xmlns:cdr = "cips.nci.nih.gov/cdr"
                              version = "1.0">

 <xsl:output                   method = "xml"
                 omit-xml-declaration = "no"/>

 <!--
 ================================================================
 Passed in by the caller.
 ================================================================ -->
 <xsl:param                      name = "changeFrom"/>
 <xsl:param                      name = "changeTo"/>


 <!--
 ================================================================
 Default rule, copying back out everything just as we got it.
 ================================================================ -->
 <xsl:template                  match = "@*|comment()|*|
                                         processing-instruction()|text()">
  <xsl:copy>
   <xsl:apply-templates        select = "@*|comment()|*|
                                         processing-instruction()|text()"/>
  </xsl:copy>
 </xsl:template>

 <!--
 ===================================================================
 Template to change the Organization link
 There are four possible options for changing an Organization link:
 a)  Value in Document 11111
     changeFrom        11111
     ==> change to 22222 or 22222#F2 depending on user entry
 b)  Value in Document 11111#F1
     changeFrom        11111
     ==> change to 22222 or 22222#F2 depending on user entry
 c)  Value in Document 11111
     changeFrom        11111#F1
     ==> no change will be done. If the user specified the fragment
         specifically it has to be assumed that this fragment exists
 d)  Value in Document 11111#F1
     changeFrom        11111#F1
     ==> change to 22222 or 22222#F2 depending on user entry

 =================================================================== -->
 <xsl:template                  match = "/InScopeProtocol/ProtocolAdminInfo
                                         /ProtocolLeadOrg/ProtocolSites
                                         /OrgSite/OrgSiteID
                                         |
                                         /InScopeProtocol/ProtocolAdminInfo
                                         /ProtocolLeadOrg/ProtocolSites
                                         /PrivatePracticeSite
                                         /PrivatePracticeSiteID">
  <xsl:choose>
   <!-- Changing Organization link based on option (d) -->
   <xsl:when                     test = "contains(@cdr:ref, '#') and
                                         contains($changeFrom, '#') and
                                         @cdr:ref=$changeFrom">
    <OrgSiteID>
     <xsl:attribute              name = "cdr:ref">
      <xsl:value-of            select = "$changeTo"/>
     </xsl:attribute>
    </OrgSiteID>
   </xsl:when>

   <!-- Changing Organization link based on option (b) -->
   <xsl:when                      test = "contains(@cdr:ref, '#') and
                                          not(contains($changeFrom, '#')) and
                                          substring-before(@cdr:ref, '#')
                                          =$changeFrom">
    <OrgSiteID>
     <xsl:attribute              name = "cdr:ref">
      <xsl:value-of            select = "$changeTo"/>
     </xsl:attribute>
    </OrgSiteID>
   </xsl:when>

   <!-- Changing Organization link based on option (d) -->
   <xsl:when                     test = "not(contains(@cdr:ref, '#')) and
                                         not(contains($changeFrom, '#')) and
                                         @cdr:ref=$changeFrom">
    <OrgSiteID>
     <xsl:attribute              name = "cdr:ref">
      <xsl:value-of            select = "$changeTo"/>
     </xsl:attribute>
    </OrgSiteID>
   </xsl:when>

   <!-- All other nodes will be put out as is -->
   <xsl:otherwise>
    <xsl:copy>
     <xsl:apply-templates      select = "@*|comment()|*|
                                         processing-instruction()|text()"/>
    </xsl:copy>
   </xsl:otherwise>
  </xsl:choose>

 </xsl:template>

</xsl:transform>
