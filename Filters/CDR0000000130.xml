<?xml version = "1.0"?>
<!-- $Id$ -->
<!-- Filter title: Documentation Help Screens Filter -->

<!--Filter written by C.Burg - 3/14/2002 -->
<!-- ===========================================================
           Filter to display XMetal Help Screens
      ============================================================ -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr">

 <xsl:import    href = "cdr:name:Module: InLine Markup Formatter"/>
 <xsl:include   href = "cdr:name:Module: Table Formatter"/>
 <xsl:include   href = "cdr:name:Module: STYLE Default"/>

 <xsl:output                    method = "html"
                              encoding = "utf-8"
                                indent = "yes"/>


 <xsl:template                   match = "/">
  <xsl:text disable-output-escaping='yes'>&lt;!DOCTYPE html>
</xsl:text>
  <html>
   <head>
    <xsl:variable                 name = "cdrdocs"
                                select = "document('cdrx:/*/CdrCtl')"/>
    <title>
     <xsl:value-of              select = "document('cdrutil:/docid')"/>
     <xsl:text>: </xsl:text>
     <xsl:value-of              select = "substring-before($cdrdocs/
                                          CdrDocCtl/DocTitle,';')"/>
    </title>

    <!--Inline sytle sheet to display H2 and H4 headers in Navy -->
    <style>
     <xsl:call-template           name = "defaultStyle"/>
     body { font-family: Arial, Helvetica, sans-serif }
     h2, h3, h4 {color: Navy}
    </style>
   </head>
   <xsl:apply-templates         select = "Documentation"/>
  </html>
 </xsl:template>


 <xsl:template                   match = "Documentation">
  <body>
   <xsl:apply-templates/>
  </body>
 </xsl:template>


 <!--
 ============================================================
 Display the Title of the Help Screen
 ============================================================ -->
 <xsl:template                   match = "DocumentationTitle">
  <h2>
   <xsl:value-of                select = "."/>
  </h2>

  <xsl:apply-templates          select = "../../DateLastModified"
                                  mode = "top"/>
 </xsl:template>


 <!--
 ====================================================================
 Display a topic section title
 ====================================================================  -->
 <xsl:template                   match = "TopicSectionTitle">
  <h3>
   <xsl:value-of                select = "."/>
  </h3>
 </xsl:template>


 <!--
 ==============================================================
 Do not display these data elements
 ============================================================== -->
 <xsl:template                   match = "Metadata | DateLastModified"/>


 <!--
 ==============================================================
 Display Related Topic
 ============================================================== -->
 <xsl:template                   match = "TopicRelationship">
  <xsl:for-each                 select = "//RelatedTopic">
   <b>
    <xsl:value-of               select = "@RelationshipType"/>
    <xsl:text>:</xsl:text>
   </b>
   <br />
   <xsl:for-each                select = "RelatedTopicID">
    <a>
     <xsl:attribute               name = "href">
      <xsl:value-of select = "concat('/cgi-bin/cdr/Filter.py?DocId=', @cdr:href,
                       '&amp;Filter=name:Documentation+Help+Screens+Filter')"/>
     </xsl:attribute>
     <xsl:value-of              select = "."/>
     <br />

     <xsl:if                      test = "position() = last()">
      <br />
     </xsl:if>
    </a>
   </xsl:for-each>
  </xsl:for-each>
 </xsl:template>


 <!--
 ====================================================================
 Display the last modified date on top of the document.
 It is displayed as "Last Reviewed" to give an incentive of updating
 a document with an old date.
 ====================================================================  -->
 <xsl:template                   match = "DateLastModified"
                                  mode = "top">
  <b>
   <xsl:text>Last Reviewed: </xsl:text>
  </b>
  <xsl:value-of                 select = "."/>
 </xsl:template>


 <!--
 =====================================================================
 Display a Note element without markup
 ===================================================================== -->
 <xsl:template               match = "Note">
  <i>
   <font color="green">
    <xsl:text> [Note: </xsl:text>
    <xsl:apply-templates/>
    <xsl:text>] </xsl:text>
   </font>
  </i>
 </xsl:template>

 <!--
 =====================================================================
 Display a Tip element in a separate paragraph.
 ===================================================================== -->
 <xsl:template               match = "Tip">
  <p style="color: green; font-style: italic">
    <xsl:text>TIP: </xsl:text>
    <xsl:apply-templates/>
  </p>
 </xsl:template>

</xsl:transform>
