<?xml version = "1.0" encoding="utf-8"?>
<!-- $Id$ -->
<!-- Filter title: InScopeProtocol HP Citations QC Report -->
<!--
================================================================
$Id$

Filter to generate a Protocol Citations QC Report
Written by Cheryl Burg

BZIssue::5166 - QC reports showing in table format

================================================================ -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr"
                               version = "1.0">

 <xsl:output                    method = "html"
                  omit-xml-declaration = "no"
                              encoding = "utf-8"/>
  
 <xsl:include  href = "cdr:name:Module: Attribute Sets"/>
 <xsl:include  href = "cdr:name:Module: InScopeProtocol HP Identification"/>
 <xsl:include  href = "cdr:name:Module: InScopeProtocol HP Abstract"/>
 <xsl:include  href = "cdr:name:Module: InScopeProtocol HP Details"/>
 <xsl:include  href = "cdr:name:Module: Inline Markup Formatter"/>
 <xsl:include  href = "cdr:name:Module: QC Filter Templates"/>
 <xsl:include  href = "cdr:name:Module: STYLE Default"/>

 <xsl:variable                    name = "docID"
                                select = "document('cdrutil:/docid')"/>
 <xsl:variable                    name = "whoAmI"
                                select = "'ProtCitationQc'"/>

 <!-- 
 ===========================================================
 Generate the report title and date 
 =========================================================== -->
 <xsl:template                   match = "/"> 
  <xsl:text  disable-output-escaping = "yes"
>&lt;!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN"
          "http://www.w3.org/TR/html4/strict.dtd"&gt;
</xsl:text>
  <HTML>
   <HEAD>
    <xsl:variable                 name = "cdrdocs" 
                                select = "document('cdrx:/*/CdrCtl')"/>
    <TITLE>
     <xsl:value-of              select = "concat('CDR', 
                                           number(
                                           substring-after($docID, 'CDR')))"/>
     <xsl:text>: </xsl:text>
     <xsl:value-of              select = "substring-before(
                                           concat($cdrdocs/CdrDocCtl/
                                                  DocTitle, ';'), ';')"/> 
    </TITLE>
    <STYLE type='text/css'>
     <xsl:call-template           name = "defaultStyle"/>  
     ul.term         {margin-left: 16 ; padding-left: 0;}
    </STYLE>
   </HEAD>
   <BASEFONT FACE='Arial, Helvetica, sans-serif' /> 
   <xsl:apply-templates         select = "InScopeProtocol"/>
  </HTML>
 </xsl:template>

 <!-- 
 ====================================================================
 Main Protocol Template
 ==================================================================== -->
 <xsl:template                   match = "InScopeProtocol">
  <BODY>
   <b>
    <font size='4'>
     <center>Protocol Citations<br />QC Report</center>
    </font>
    <center>
     <xsl:value-of              select = "document('cdrutil:/date/%25B %25#d, %25Y %25#I:%25M%25p')"/>
    </center>
   </b>
   <br />

   <!-- 
   ====================================================================
   Display CDR ID, Protocol ID ( Primary and Others )
   ==================================================================== -->
   <b>
    <font size='4'>
     <xsl:value-of              select = "concat('CDR', 
                                          number(
                                          substring-after($docID, 'CDR')))"/>
    </font>
   </b>
   <p/>  

   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <u>
       <b>
        <font size='4'>Protocol ID</font>
       </b>
      </u>
     </td>
    </tr>
   </table>

   <table       xsl:use-attribute-sets = "table"> 
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Primary ID</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "ProtocolIDs/
                                          PrimaryID/
                                          IDString"/>
     </td>
    </tr>
   </table>

   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Other IDs</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:for-each             select = "ProtocolIDs/OtherID">
       <xsl:value-of            select = "IDString"/>
       <xsl:if                    test = "position() != last()">
        <xsl:text>,&#xa0;</xsl:text>
       </xsl:if>
      </xsl:for-each>
     </td>
    </tr>
   </table>
   <br/>

   <!-- 
   ==============================================================
   Display Protocol Titles
   ============================================================== -->
   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>
       <font size='4'>Original Protocol Title</font>
      </b>
     </td>
     <td class="qc_td">
      <xsl:apply-templates      select = "ProtocolTitle[@Type = 'Original']"/>
     </td>
    </tr>
   </table>
   <br />

   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>
       <font size='4'>Professional Title</font>
      </b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:apply-templates      select = "ProtocolTitle[@Type = 
                                                          'Professional']"/>
     </td>
    </tr>
   </table>
   <br />

   <!-- 
   ================================================================
   Display Protocol Chairman
   ================================================================ -->
   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>
       <font size='4'>Protocol Chairman</font>
      </b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:for-each             select = "ProtocolAdminInfo/
                                          ProtocolLeadOrg/
                                          LeadOrgPersonnel/
                                          Person/
                                          Person/
                                          PersonNameInformation">
                                       


       <xsl:if                    test = "../../
                                          ../../
                                          LeadOrgRole = 'Primary'">
        <xsl:value-of           select = "SurName"/>,
        <xsl:value-of           select = "GivenName"/>
       </xsl:if>
       <br />
      </xsl:for-each>
     </td>
    </tr>
   </table>
   <hr />

   <!-- 
   ==============================================================
   Display Published Results/Format Citation 
   ============================================================== -->
   <b>
    <i>
     <font size='4'>Published Results</font>
    </i>
   </b>
   <p/>
   <xsl:apply-templates         select = "PublishedResults"/>
   <hr />

   <!-- 
   ==============================================================
   Display Published Results/Format Citation 
   ============================================================== -->
   <b>
    <i>
     <font size='4'>Related Publications</font>
    </i>
   </b>
   <p/>
   <xsl:apply-templates         select = "RelatedPublications"/>
   <hr />

   <xsl:apply-templates         select = "ProtocolAbstract/
                                          Professional/
                                          Objectives"/>
   <xsl:apply-templates         select = "ProtocolAbstract/
                                          Professional/
                                          ProjectedAccrual"/>
   <xsl:apply-templates         select = "ProtocolAbstract/
                                          Professional/
                                          Outline"/>
   <xsl:apply-templates         select = "ProtocolDetail"/>
   <xsl:apply-templates         select = "ProtocolPhase"/>
  </BODY>
 </xsl:template>


 <!-- 
 ================================================================
 Display Citations
 ================================================================ -->
 <xsl:template                   match = "PublishedResults |
                                          RelatedPublications">
  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Citation ID</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "concat('CDR', number(
                                          substring-after(@cdr:ref, 'CDR')))"/>
    
    </td>
   </tr>
  </table>
 
  <xsl:if                         test = "@PMID">
   <table       xsl:use-attribute-sets = "table">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>PUBMED ID</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:element                name = "a">
       <xsl:attribute             name = "href">
        <xsl:value-of           select = "concat('http://www.ncbi.nlm.nih.gov/',
                                                 'entrez/query.fcgi?',
                                                 'cmd=Retrieve',
                                                 '&amp;db=pubmed',
                                                 '&amp;dopt=Abstract',
                                                 '&amp;list_uids=',
                                                 @PMID)"/>
       </xsl:attribute>
       <xsl:attribute             name = "target">
        <xsl:text>new</xsl:text>
       </xsl:attribute>

      <xsl:value-of             select = "@PMID"/>
      </xsl:element>
     </td>
    </tr>
   </table>
  </xsl:if>

  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Citation</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "."/>
    </td>
   </tr>
  </table>
  <xsl:if                         test = "position() != last()">
   <hr width="25%"/>
  </xsl:if>
 </xsl:template>  


 <!-- 
 ================================================================
 Display a Note
 ================================================================ -->
 <xsl:template                   match = "Note">
  <xsl:apply-templates          select = "." 
                                  mode = "RS"/>
 </xsl:template>  
</xsl:transform>
