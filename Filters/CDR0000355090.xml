<CdrDoc Type='Filter' Id='CDR0000355090'>
<CdrDocCtl>
<DocValStatus readonly="yes">U</DocValStatus>
<DocActiveStatus readonly="yes">A</DocActiveStatus>
<DocTitle readonly="yes">Emailer Site Info</DocTitle>
<ReadyForReview readonly="yes">N</ReadyForReview>
</CdrDocCtl>
<CdrDocXml><![CDATA[<?xml version="1.0"?>

<!--
     $Id$

     $Log: not supported by cvs2svn $
  -->

<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr"
                               version = "1.0">
 <xsl:output                    method = "xml"
                  omit-xml-declaration = "yes"/>

 <xsl:template                   match = "/">
  <xsl:apply-templates          select = "Organization"/>
 </xsl:template>

 <xsl:template                   match = "Organization">
  <Organization>
   <xsl:attribute                 name = "id">
    <xsl:value-of               select = "document('cdrutil:/docid')"/>
   </xsl:attribute>
   <xsl:apply-templates         select = "OrganizationNameInformation"/>
   <xsl:apply-templates         select = "OrganizationLocations"/>
  </Organization>
 </xsl:template>

 <xsl:template                   match = "OrganizationNameInformation">
  <xsl:for-each                 select = "OfficialName/Name">
   <Name>
    <xsl:value-of               select = "normalize-space()"/>
   </Name>
  </xsl:for-each>
 </xsl:template>

 <xsl:template                   match = "OrganizationLocations">
  <xsl:for-each                 select = "CIPSContact">
   <CIPSContact>
    <xsl:value-of               select = "normalize-space()"/>
   </CIPSContact>
  </xsl:for-each>
  <xsl:apply-templates          select = "OrganizationLocation/Location"/>
 </xsl:template>

 <xsl:template                   match = "Location">
  <Location>
   <xsl:attribute                 name = "id">
    <xsl:value-of               select = "@cdr:id"/>
   </xsl:attribute>
   <xsl:apply-templates         select = "PostalAddress"/>
  </Location>
 </xsl:template>

 <xsl:template                   match = "PostalAddress">
  <xsl:for-each                 select = "City">
   <City>
    <xsl:value-of               select = "normalize-space()"/>
   </City>
  </xsl:for-each>
  <xsl:for-each                 select = "PoliticalSubUnit_State">
   <xsl:variable                  name = "stateId"
                                select = "@cdr:ref"/>
   <xsl:variable                  name = "stateUri"
                                select = "concat('cdr:', $stateId)"/>
   <xsl:variable                  name = "state"
                                select = "document($stateUri)"/>
   <xsl:variable                  name = "stateName"
                                select = "$state/PoliticalSubUnit/
                                          PoliticalSubUnitFullName"/>
   <State                           id = "{$stateId}">
    <xsl:value-of               select = "normalize-space($stateName)"/>
   </State>
  </xsl:for-each>
  <xsl:for-each                 select = "Country">
   <xsl:variable                  name = "countryId"
                                select = "@cdr:ref"/>
   <xsl:variable                  name = "countryUri"
                                select = "concat('cdr:', $countryId)"/>
   <xsl:variable                  name = "country"
                                select = "document($countryUri)"/>
   <xsl:variable                  name = "countryName"
                                select = "$country/Country/CountryFullName"/>
   <Country                         id = "{$countryId}">
    <xsl:value-of               select = "normalize-space($countryName)"/>
   </Country>
  </xsl:for-each>
 </xsl:template>

</xsl:transform>]]>
</CdrDocXml>
</CdrDoc>