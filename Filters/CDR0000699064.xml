<?xml version = "1.0" encoding="utf-8"?>
<!-- Filter title: Module: Citation Denormalization - QD -->
<!--
================================================================
Module: Quick and Dirty Citation Denormalization
Creator:  Volker Englisch
Created:  2011-02-24

Quick and Dirty version of the Filter "Module: Citation
Denormalization".
The users want to be able to "force" completion of a QC report
even in the even that a denormalization error occurrs.

BZIssue::4990 - New "Quick & Dirty" QC report

================================================================ -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr">

 <xsl:output                    method = "xml"
                  omit-xml-declaration = "no"
                              encoding = "UTF-8"/>


 <!-- *
 ==============================================================
 Template to denormalize Protocol Published Results section and
 Summary Reference Section
 ============================================================== -->
 <xsl:template                   match = "Citation | CitationLink">

  <!--
  If an empty CitationLink has been entered as a placeholder we need
  to avoid denormalization or the report will fail (since there is no
  cdr:ref available to denormalize with).
  <xsl:if                        test = "@cdr:ref != ''">
   <xsl:variable                 name = "pubVersion">
    <xsl:text>-1</xsl:text>
   </xsl:variable>
  =================================================================== -->
  <xsl:variable                   name = "pubVersion">
   <xsl:choose>
    <xsl:when                     test = "@cdr:ref">
     <xsl:value-of              select = "document(concat('cdrutil:',
                                                      '/get-pv-num/',
                                                      @cdr:ref))"/>
    </xsl:when>
    <xsl:otherwise>
     <xsl:text>-1</xsl:text>
    </xsl:otherwise>
   </xsl:choose>
  </xsl:variable>

  <xsl:variable                   name = "citInfo">
   <xsl:choose>
    <xsl:when                     test = "$pubVersion = '0'
                                          or
                                          $pubVersion = '-1'">
     <xsl:text>*** Denormalization Error: </xsl:text>
     <xsl:value-of              select = "name()"/>
     <xsl:text> ***</xsl:text>
    </xsl:when>
    <xsl:otherwise>
     <xsl:copy-of               select = "document(concat('cdr:', @cdr:ref,
                                                                  '/lastp'))"/>
    </xsl:otherwise>
   </xsl:choose>
  </xsl:variable>

  <Citation>
   <xsl:for-each                select = "@*">
    <xsl:attribute                name = "{name()}">
     <xsl:value-of              select = "."/>
    </xsl:attribute>
   </xsl:for-each>

   <xsl:variable                  name = "citRef"
                                select = "@cdr:ref"/>



   <xsl:apply-templates         select = "$citInfo/Citation/PDQCitation |
                                          $citInfo/Citation/PubmedArticle"/>
  </Citation>

  <!--
  Report an error if we couldn't find a publishable version or the
  cdr:ref ID is empty
  ================================================================ -->
  <xsl:if                         test = "$pubVersion = '-1'">
   <LinkError>
    <xsl:value-of               select = "$citInfo"/>
   </LinkError>
  </xsl:if>
 </xsl:template>


 <!-- *
 =================================================================
 Template to denormalize ProtocolLink elements for Summary
 Reference section
 (Need to address both flavors: InScopeProtocol and CTGovProtocol)
 ================================================================= -->
 <xsl:template                  match = "ProtocolLink">
  <ProtocolLink>
   <xsl:for-each               select = "@*">
    <xsl:attribute               name = "{name()}">
     <xsl:value-of             select = "."/>
    </xsl:attribute>
   </xsl:for-each>

   <xsl:variable                 name = "protRef"
                               select = "@cdr:ref"/>
   <xsl:variable                 name = "protInfo"
                               select = "document(concat('cdr:', $protRef,
                                                            '/lastp'))"/>
   <!--
   This new attribute is introduced in order to sort by publication
   year across different citation types
   ================================================================ -->
   <xsl:attribute                name = "pubyear">
    <xsl:value-of              select = "$protInfo/xxx/Citation/PDQCitation/
                                         PublicationDetails/
                                         PublicationYear      |
                                         $protInfo/xxx/Citation/PubmedArticle//
                                         PubDate/Year"/>
   </xsl:attribute>

   <xsl:apply-templates        select = "$protInfo/InScopeProtocol/
                                           ProtocolTitle
                                             [@Type = 'Professional'] |
                                         $protInfo/InScopeProtocol/
                                           ProtocolIDs                |
                                         $protInfo/InScopeProtocol/
                                           ProtocolAdminInfo/
                                           ProtocolLeadOrg            |
                                         $protInfo/InScopeProtocol/
                                           ProtocolDetail/
                                           StudyType                  |
                                         $protInfo/InScopeProtocol/
                                           ProtocolAdminInfo/
                                           CurrentProtocolStatus      |
                                         $protInfo/CTGovProtocol/
                                           OfficialTitle              |
                                         $protInfo/CTGovProtocol/
                                           IDInfo                     |
                                         $protInfo/CTGovProtocol/
                                           PDQIndexing/StudyType      |
                                         $protInfo/CTGovProtocol/
                                           OverallStatus"/>

  </ProtocolLink>
 </xsl:template>


 <!--
 ================================================================
 Template to denormalize Person and Organization for Protocol
 Citation
 ================================================================ -->
 <xsl:template                  match = "ProtocolLeadOrg">
  <ProtocolLeadOrg>
   <xsl:apply-templates/>
  </ProtocolLeadOrg>
 </xsl:template>


 <!-- *
 ================================================================
 Template to denormalize the Protocol Person elements for Summary
 Reference section
 ================================================================ -->
 <xsl:template                  match = "Person">
  <Person>
   <xsl:for-each               select = "@*">
    <xsl:attribute               name = "{name()}">
     <xsl:value-of             select = "."/>
    </xsl:attribute>
   </xsl:for-each>

   <xsl:variable                 name = "personRef"
                               select = "@cdr:ref"/>
   <xsl:variable                 name = "personInfo"
                               select = "document(concat('cdr:', $personRef,
                                                            '/last'))"/>

   <xsl:apply-templates        select = "$personInfo/Person/
                                           PersonNameInformation"/>

  </Person>
 </xsl:template>


 <!-- *
 ==============================================================
 Template to denormalize the Protocol Org elements for Summary
 Reference section
 ============================================================== -->
 <xsl:template                  match = "LeadOrganizationID">
  <Organization>
   <xsl:for-each               select = "@*">
    <xsl:attribute               name = "{name()}">
     <xsl:value-of             select = "."/>
    </xsl:attribute>
   </xsl:for-each>

   <xsl:variable                 name = "orgRef"
                               select = "@cdr:ref"/>
   <xsl:variable                 name = "orgInfo"
                               select = "document(concat('cdr:', $orgRef,
                                                            '/last'))"/>

   <xsl:apply-templates        select = "$orgInfo/Organization/
                                           OrganizationNameInformation"/>

  </Organization>
 </xsl:template>



 <!-- *
 ==============================================================
 Template PublishedIn section
 ============================================================== -->
 <xsl:template                  match = "PublishedIn">
  <xsl:element                   name = "{name()}">
   <xsl:for-each               select = "@*">
    <xsl:attribute               name = "{name()}">
     <xsl:value-of             select = "."/>
    </xsl:attribute>
   </xsl:for-each>

   <xsl:variable                 name = "citRef"
                               select = "@cdr:ref"/>
   <xsl:variable                 name = "citInfo"
                               select = "document(concat('cdr:', $citRef,
                                                            '/lastp'))"/>
   <xsl:copy-of                select = "$citInfo/Citation"/>

  </xsl:element>
 </xsl:template>

</xsl:transform>
