<?xml version="1.0" ?>
<!-- Filter title: Module: Global Filter Templates -->
<!--
================================================================
Filter modules for general use (string conversion, etc.)

Created: Volker Englisch
================================================================ -->
<xsl:transform           xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                         xmlns:cdr = "cips.nci.nih.gov/cdr"
           exclude-result-prefixes = "cdr"
                           version = "1.0">

  <xsl:output               method = "xml"
              omit-xml-declaration = "no"
                          encoding = "utf-8"/>


 <!-- *
 ====================================================================
 Assemble the PersonNameInformation for a person.
 (Eliminating the StandardProfessionalSuffix element)
 ==================================================================== -->
 <xsl:template               match = "PersonNameInformation">
   <PersonNameInformation>
    <xsl:apply-templates    select = "GivenName"
                              mode = "copy"/>
    <xsl:apply-templates    select = "MiddleInitial"
                              mode = "copy"/>
    <xsl:apply-templates    select = "SurName"
                              mode = "copy"/>
    <xsl:apply-templates    select = "Prefix"
                              mode = "copy"/>
    <xsl:apply-templates    select = "GenerationSuffix"
                              mode = "copy"/>

    <!--
    If the ProfessionalSuffix element is prepared for the vendor output
    it's created from the Standard/Custom-ProfessionalSuffix elements
    (when clause).
    However, if the ProfessionalSuffic element is only to be copied -
    because it's the second pass of this filter module - the element
    only needs to be copied as is (otherwise clause).
    ==================================================================== -->
    <xsl:choose>
     <xsl:when                test = "ProfessionalSuffix/
                                        StandardProfessionalSuffix |
                                      ProfessionalSuffix/
                                        CustomProfessionalSuffix">
      <ProfessionalSuffix>
       <xsl:for-each        select = "ProfessionalSuffix/
                                        StandardProfessionalSuffix |
                                      ProfessionalSuffix/
                                        CustomProfessionalSuffix">
        <!-- Clean up the periods of the Custom Prof Suffix  -->
        <xsl:choose>
         <xsl:when            test = "name() = 'CustomProfessionalSuffix'">
          <xsl:value-of     select = "translate(node(), '.', '')"/>
         </xsl:when>
         <xsl:otherwise>
          <xsl:value-of     select = "."/>
         </xsl:otherwise>
        </xsl:choose>
        <xsl:if               test = "position() != last()">
         <xsl:text>, </xsl:text>
        </xsl:if>
       </xsl:for-each>
      </ProfessionalSuffix>
     </xsl:when>
     <xsl:otherwise>
      <xsl:apply-templates  select = "ProfessionalSuffix"
                              mode = "copy"/>
     </xsl:otherwise>
    </xsl:choose>

    <xsl:apply-templates    select = "Comment"
                              mode = "copy"/>
    <xsl:apply-templates    select = "NameFormat"
                              mode = "copy"/>
   </PersonNameInformation>
 </xsl:template>


<!-- ======================================================================
        Generate DateFirstPublished element only if the passed-in value
        for parameter date is non-empty.
     ===================================================================== -->
  <xsl:template                   name = "getDateFirstPub">
    <xsl:param                    name = "date"/>

    <xsl:if                       test = "not($date='')">
      <DateFirstPublished>
        <xsl:value-of           select = "$date"/>
      </DateFirstPublished>
    </xsl:if>
  </xsl:template>


<!-- ======================================================================
     Drop white space node between 2 consecutive CitationLink/ProtocolLink
     elements.
     ===================================================================== -->
  <xsl:template                   name = "stripSpaceNode">
    <xsl:variable                 name = "prev"
                                select = "preceding-sibling::*[position()=1]"/>
    <xsl:variable                 name = "next"
                                select = "following-sibling::*[position()=1]"/>

    <xsl:if                       test = "(name($prev) != 'CitationLink'
                                           and name($prev) != 'ProtocolLink')
                                       or (name($next) != 'CitationLink'
                                           and name($next) != 'ProtocolLink')">
      <xsl:value-of    select = "."/>
    </xsl:if>
  </xsl:template>


 <!--
 =====================================================================
 Template to convert a text string
 In the calling template you need to specify a parameter
   setCase = 'value'
 with valid value entries upper, lower, proper.
 ===================================================================== -->
 <xsl:template                    name = "convertCase">
  <xsl:param                      name = "convString"/>
  <xsl:param                      name = "convertTo"/>
 <xsl:variable name = "lcLetters">abcdefghijklmnopqrstuvwxyz</xsl:variable>
 <xsl:variable name = "ucLetters">ABCDEFGHIJKLMNOPQRSTUVWXYZ</xsl:variable>


  <xsl:choose>
   <xsl:when                      test = "$convertTo = 'lower'">
    <xsl:value-of               select = "translate($convString,
                                                    $ucLetters,$lcLetters)" />
   </xsl:when>
   <xsl:when                      test = "$convertTo = 'upper'">
    <xsl:value-of               select = "translate($convString,
                                                    $lcLetters,$ucLetters)" />
   </xsl:when>
   <xsl:when                      test = "$convertTo = 'proper'">
    <xsl:call-template            name = "convertProperCase">
     <xsl:with-param              name = "convString">
      <xsl:value-of             select = "translate($convString,
                                                    $ucLetters,$lcLetters)" />
     </xsl:with-param>
    </xsl:call-template>
   </xsl:when>
   <xsl:otherwise>
    <xsl:value-of               select = "$convString"/>
   </xsl:otherwise>
  </xsl:choose>
 </xsl:template>


 <!--
 =====================================================================
 Template to convert a text string to propper case
 ===================================================================== -->
 <xsl:template                    name = "convertProperCase">
  <xsl:param                      name = "convString"/>

  <xsl:if                         test = "string-length($convString) > 0">
   <xsl:variable                  name = "firstChar"
                                select = "substring($convString, 1, 1)"/>
   <xsl:variable                  name = "nextChars"
                                select = "substring($convString, 2)"/>

   <xsl:call-template             name = "convertCase">
    <xsl:with-param               name = "convString"
                                select = "$firstChar" />
    <xsl:with-param               name = "convertTo"
                                select = "'upper'"/>
   </xsl:call-template>

   <xsl:choose>
    <xsl:when                     test = "contains($nextChars,' ')">
     <xsl:value-of              select = "substring-before($nextChars,' ')"/>
     <xsl:text> </xsl:text>
     <xsl:call-template           name = "convertProperCase">
      <xsl:with-param             name = "convString"
                                select = "substring-after($nextChars,' ')"/>
     </xsl:call-template>
    </xsl:when>

    <xsl:otherwise>
     <xsl:value-of              select = "$nextChars"/>
    </xsl:otherwise>
   </xsl:choose>
  </xsl:if>
 </xsl:template>


 <!--
 ========================================================================
 Create the URLInfo block for InScopeProtocol and CTGovProtocol
 ======================================================================== -->
 <xsl:template                 name = 'urlInfo'>
  <xsl:element                 name = 'UrlInfo'>
   <xsl:element                name = 'PrimaryUrlID'>
    <xsl:value-of            select = 'IDInfo/
                                       NCTID        |
                                       ProtocolIDs/
                                       PrimaryID/
                                       IDString'/>
   </xsl:element>
   <xsl:if                     test = 'PDQAdminInfo/
                                       PDQProtocolIDs/
                                       PrimaryID/
                                       IDString'>
    <xsl:element               name = 'SecondaryUrlID'>
     <xsl:value-of           select = 'PDQAdminInfo/
                                       PDQProtocolIDs/
                                       PrimaryID/
                                       IDString'/>
    </xsl:element>
   </xsl:if>
   <xsl:if                     test = 'ProtocolIDs/
                                       OtherID/
                                       IDType = "ClinicalTrials.gov ID"'>
    <xsl:element               name = 'SecondaryUrlID'>
     <xsl:value-of           select = 'ProtocolIDs/
                                       OtherID[IDType="ClinicalTrials.gov ID"]/
                                       IDString'/>
    </xsl:element>
   </xsl:if>
  </xsl:element>
 </xsl:template>

<!--
 <xsl:template                 name = 'urlInfo'>
  <xsl:variable                name = 'ctGovIDs'>
   <xsl:for-each             select = '/CTGovProtocol/
                                        IDInfo/
                                        NCTID          |
                                       /CTGovProtocol/
                                        IDInfo/
                                        OrgStudyID     |
                                       /CTGovProtocol/
                                        IDInfo/
                                        SecondaryID    |
                                       /InScopeProtocol/
                                        ProtocolIDs/
                                        PrimaryID/
                                        IDString       |
                                       /InScopeProtocol/
                                        ProtocolIDs/
                                        OtherID/
                                        IDString'>
     <xsl:value-of           select = '.'/>
     <xsl:text>~</xsl:text>
   </xsl:for-each>
   <xsl:text>~</xsl:text>
  </xsl:variable>

  <xsl:variable                name = 'pdqIDs'>
   <xsl:if                     test = '/CTGovProtocol/
                                        PDQProtocolIDs'>
    <xsl:for-each            select = 'PDQProtocolIDs/*/IDString'>
     <xsl:value-of           select = '.'/>
     <xsl:text>~</xsl:text>
    </xsl:for-each>
   </xsl:if>
  </xsl:variable>

  <xsl:variable                name = 'allIDs'
                             select = 'concat($ctGovIDs, $pdqIDs)'/>

  <xsl:value-of              select = '$allIDs'/>
  <xsl:variable                name = 'newIDs'
                             select = "document(concat('cdrutil:/dedup-ids/',
                                                         $allIDs))"/>

  <UrlInfo>
   <xsl:element                name = 'PrimaryUrlID'>
    <xsl:value-of            select = '/CTGovProtocol/
                                        IDInfo/
                                        NCTID  |
                                       /InScopeProtocol/
                                        ProtocolIDs/
                                        PrimaryID/
                                        IDString'/>
   </xsl:element>
   <xsl:for-each             select = '/CTGovProtocol/
                                        IDInfo/
                                        OrgStudyID     |
                                       /CTGovProtocol/
                                        IDInfo/
                                        SecondaryID    |
                                       /InScopeProtocol/
                                        ProtocolIDs/
                                        OtherID/
                                        IDString'>
    <xsl:element               name = 'SecondaryUrlID'>
     <xsl:value-of           select = '.'/>
    </xsl:element>
   </xsl:for-each>
   <xsl:if                     test = '$newIDs/result/id'>
    <xsl:for-each            select = '$newIDs/result/id'>
     <xsl:element              name = 'SecondaryUrlID'>
      <xsl:value-of          select = '.'/>
     </xsl:element>
    </xsl:for-each>
   </xsl:if>

   <xsl:if                     test = '/InScopeProtocol/
                                        ProtocolIDs/
                                        OtherID/
                                        IDType = "ClinicalTrials.gov ID"'>
     <xsl:element              name = 'SecondaryUrlID'>
      <xsl:value-of          select = '/InScopeProtocol/
                                        ProtocolIDs/
                                        OtherID/
                                        IDType["ClinicalTrials.gov ID"]/
                                        IDString'/>
     </xsl:element>
   </xsl:if>
  </UrlInfo>
 </xsl:template>
-->


<!-- ======================================================================
     Template rule used to do XML to XML transformations which
     copies any attribute node, or node that is child of of something
     ===================================================================== -->
  <xsl:template                  match = "@*"
                                  mode = "copy">
    <xsl:copy/>
  </xsl:template>

  <xsl:template                  match = "*"
                                  mode = "copy">
    <xsl:element                  name = "{name()}">
      <xsl:apply-templates      select = "@*"
                                  mode = "copy"/>
      <xsl:apply-templates        mode = "copy"/>
    </xsl:element>
  </xsl:template>

</xsl:transform>
