<?xml version="1.0"?>
<!-- Filter title: Vendor Filter: Country -->
<!--======================================================================
             Filter to create Vendor XML for Country
    ====================================================================== -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr">

 <xsl:include  href = "cdr:name:Module: Vendor Filter Templates"/>

 <xsl:output                    method = "xml"
                  omit-xml-declaration = "no"/>

 <!-- Stylesheet level parameter DateFirstPub, passed in from the caller -->
 <xsl:param                       name = "DateFirstPub"
                                select = "''"/>

 <!-- *
 ====================================================================
 Root element.
 ==================================================================== -->
 <xsl:template                   match = "*">
  <xsl:apply-templates/>
 </xsl:template>


 <!--
 ========================================================================
 Copy DocId, CountryFullName, Continent, and PostalCodePosition
 ======================================================================== -->
 <xsl:template                   match = "Country">
  <xsl:element                    name = '{name()}'>
   <xsl:for-each                select = "document('cdrutil:/docid')">
    <xsl:attribute                name = "id">
     <xsl:value-of              select = "."/>
    </xsl:attribute>
   </xsl:for-each>
   <xsl:if                        test = "PdqKey">
    <xsl:attribute                name = "LegacyPDQID">
     <xsl:value-of              select = "PdqKey"/>
    </xsl:attribute>
   </xsl:if>

   <xsl:apply-templates         select = "CountryFullName"
                                  mode = "copy"/>
   <xsl:apply-templates         select = "CountryShortName"
                                  mode = "copy"/>
   <xsl:apply-templates         select = "CountryAlternativeName"
                                  mode = "copy"/>
   <xsl:apply-templates         select = "Continent"
                                  mode = "copy"/>
   <xsl:apply-templates         select = "PostalCodePosition"
                                  mode = "copy"/>
   <xsl:variable                  name = "CtrlBlk"
                                select = "document('cdr:/*/CdrCtl')"/>

   <xsl:choose>
    <xsl:when                     test = "$CtrlBlk/CdrDocCtl/FirstPub/Date">
      <DateFirstPublished>
      <xsl:value-of             select = "substring-before($CtrlBlk/
                                            CdrDocCtl/FirstPub/Date,'T')"/>
      </DateFirstPublished>
    </xsl:when>
    <xsl:otherwise>
      <xsl:call-template          name = "getDateFirstPub">
        <xsl:with-param           name = "date"
                                select = "$DateFirstPub"/>
      </xsl:call-template>
    </xsl:otherwise>
   </xsl:choose>

   <xsl:if                        test = "DateLastModified">
    <xsl:apply-templates        select = "DateLastModified"
                                  mode = "copy"/>
   </xsl:if>
  </xsl:element>
 </xsl:template>

</xsl:transform>
