<?xml version="1.0" ?>
<!-- Filter title: Denormalization Filter (1/1): Political SubUnit -->
<!--
=====================================================================
Filter to Denormalize Political SubUnit XML
===================================================================== -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr">

 <xsl:output                    method = "xml"
                  omit-xml-declaration = "no"/>

 <!--
 =================================================================
 Copy control information and generate current date
 ================================================================= -->
  <xsl:template                  match = "PoliticalSubUnit">
   <xsl:copy>
   <xsl:apply-templates         select = "document('cdrutil:/docid')"/>
   <xsl:apply-templates         select = "document('cdrx:/*/CdrCtl')"/>

   <xsl:apply-templates/>

  </xsl:copy>
 </xsl:template>


 <!--
 =================================================================
 Template to denormalize Country link
 ================================================================= -->
 <xsl:template                   match = "Country">
  <xsl:variable                   name = "countryId"
                                select = "@cdr:ref"/>
  <xsl:variable                   name = "countryDoc"
                                select = "document(concat('cdr:', $countryId,
                                                          '/lastp'))"/>
  <Country>
   <xsl:for-each                select = "@*">
    <xsl:attribute                name = "{name()}">
     <xsl:value-of              select = "."/>
    </xsl:attribute>
   </xsl:for-each>
   <xsl:apply-templates         select = "$countryDoc/Country/
                                                      CountryFullName |
                                          $countryDoc/Country/
                                                      Continent       |
                                          $countryDoc/Country/
                                                      PostalCodePosition"/>
  </Country>
 </xsl:template>


 <!--
 ==================================================================
 Template rule used to do XML to XML transformations which
 copies any attribute node, or node that is child of of something
 ================================================================== -->
 <xsl:template                   match = "@*|node()">
  <xsl:copy>
   <xsl:apply-templates         select = "@*|node()"/>
  </xsl:copy>
 </xsl:template>

</xsl:transform>
