<?xml version="1.0"?>
<!-- Filter title: Copy XML for Summary Patient Report -->
<!--
 ================================================================
     $Id$

     Intermediate Filter to prepare for Patient QC Summary Report
        Written by Volker Englisch, 2002-05-06

     $Log: not supported by cvs2svn $
 ================================================================ -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr">
 <xsl:output                    method = "xml" 
                  omit-xml-declaration = "no"/>

 <!--
 ================================================================
 Main Template
 ================================================================ -->
 <xsl:template                   match = "Summary">
  <xsl:copy>
   <!--
   ==========================
   only do Patient Summaries
   ========================== -->
   <xsl:if                        test = "SummaryMetaData/
                                          SummaryAudience = 'Patients'">

    <!--
    ==================================
    copy summary meta data, and title
    ================================== -->
    <SummaryMetaData>
     <xsl:apply-templates       select = "SummaryMetaData/SummaryType"   
                                  mode = "copy"/>
     <xsl:apply-templates       select = "SummaryMetaData/SummaryAudience" 
                                  mode = "copy"/>
     <xsl:apply-templates       select = "SummaryMetadata/SummaryLanguage" 
                                  mode = "copy"/>
    </SummaryMetaData>

    <xsl:apply-templates        select = "SummaryTitle" 
                                  mode = "copy"/>

    <xsl:for-each               select = "SummarySection"> 

     <!--
     ==================================================
     The top SummarySection title needs to be displayed 
     before the keypoint section.  It is being renamed 
     here to be able and handle these titles separately
     from all other SummarySection titles
     ================================================== -->
     <SectionTitle>
      <xsl:apply-templates      select = "Title"
                                  mode = "copy"/>
     </SectionTitle>

     <!--
     =================================
     copy keypoints for keypoints box
     ================================= -->
     <KeyPoints>
      <KeyPointsList             Style = 'bullet'>
       <xsl:for-each            select = "SummarySection">
        <xsl:if                   test = "KeyPoint">
         <ListItem>
          <xsl:apply-templates  select = "KeyPoint" 
                                  mode = "copy"/>
       
          <xsl:for-each         select = "SummarySection">
           <xsl:apply-templates select = "Title" 
                                  mode = "copy"/>     
           <xsl:if                test = "KeyPoint">
            <KeyPointsList       Style = 'dash'>
             <ListItem>
              <xsl:apply-templates select = "KeyPoint" 
                                  mode = "copy"/>
             </ListItem>
            </KeyPointsList>
           </xsl:if>
          </xsl:for-each>
         </ListItem>
        </xsl:if>
       </xsl:for-each> 
      </KeyPointsList>
     </KeyPoints>
      
     <!--
     ============================================================
     copy summary sections and add references at the end of each 
     section 
     ============================================================ -->
     <xsl:apply-templates       select = "." 
                                  mode = "copy"/> 
                                            
     <xsl:apply-templates       select = "PdqKey" 
                                  mode = "copy"/>
    </xsl:for-each>

    <GlossaryTerms>
     <xsl:for-each              select = "//GlossaryTermRef">
      <GlossaryTerm>
       <GlossaryTermRef>
        <xsl:apply-templates      mode = "copy"/>
       </GlossaryTermRef>
       
       <xsl:variable              name = "defID"  
                                select = "@cdr:href"/>
      
       <xsl:variable              name = "defInfo" 
                                select = "document(concat('cdrx:', $defID,
                                                          '/last'))"/>
       <xsl:if                    test = "$defInfo/GlossaryTerm/
                                          TermDefinition/
                                          Audience = 'Patient' 
                                          and 
                                          $defInfo/GlossaryTerm/
                                          TermDefinition/
                                          Dictionary = 'Cancer.gov'"> 
                                           
        <TermName>
         <xsl:value-of          select = "$defInfo/GlossaryTerm/
                                          TermName"/> 
        </TermName>
        <DefinitionText>     
         <xsl:value-of          select = "$defInfo/GlossaryTerm/
                                          TermDefinition/DefinitionText"/>
        </DefinitionText>
       </xsl:if>
      </GlossaryTerm>
     </xsl:for-each>
    </GlossaryTerms>
   </xsl:if>
  </xsl:copy>  
 </xsl:template>


 <!-- 
 =================================================================
 Default Template rule used to do XML to XML transformations which
 copies any attribute node, or node that is child of of something
 ================================================================= -->
 <xsl:template                   match = "@*|node()" 
                                  mode = "copy">
  <xsl:copy>
   <xsl:apply-templates         select = "@*" 
                                  mode = "copy"/>
   <xsl:apply-templates           mode = "copy"/>
  </xsl:copy> 
 </xsl:template>  

</xsl:transform>
