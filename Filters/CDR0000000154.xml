<?xml version="1.0" ?>
<!-- File name:    CDR0000000154.xml      -->
<!-- Filter title: Miscellaneous Document Report Filter -->
<!-- ================================================== -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
                               version = "1.0">

 <xsl:include   href = "cdr:name:Module: QC Filter Templates - Summary"/>
 <xsl:include   href = "cdr:name:Module: Citation Formatting"/>
 <xsl:include   href = "cdr:name:Module: InLine Markup Formatter"/>
 <xsl:include   href = "cdr:name:Module: Table Formatter"/>
 <xsl:include   href = "cdr:name:Module: STYLE Default"/>
 <xsl:include   href = "cdr:name:Module: STYLE QC Summary"/>
 <xsl:include   href = "cdr:name:Module: STYLE RS Insertion Deletion"/>

 <xsl:output                    method = "html"/>

 <xsl:variable                    name = "docID"
                                select = "document('cdrutil:/docid')"/>
 <xsl:variable                    name = "whoAmI"
                                select = "'MiscDocRS'"/>
 <!--
 ================================================================
 Filter to create a Miscellaneous QC Report
 ================================================================= -->
 <xsl:template                   match = "/">
  <!--Filter written by C.Burg - 4/25/2002 -->
  <!-- Added import of InLine Markup Formatter 7/9/2002 -->
  <!-- Made Tabular 6/20/2003 - Nanci -->

  <!-- <!DOCTYPE HTML PUBLIC '-//IETF//DTD HTML//EN' /> -->
  <HTML>
   <HEAD>
    <xsl:variable                 name = "cdrdocs"
                                select = "document('cdrx:/*/CdrCtl')"/>

    <TITLE>
     <xsl:value-of              select = "concat('CDR',
                                           number(
                                           substring-after($docID, 'CDR')))"/>
     <xsl:text>: </xsl:text>
     <xsl:value-of              select = "substring-before(
                                           concat($cdrdocs/CdrDocCtl/
                                                  DocTitle, ';'), ';')"/>
    </TITLE>

    <!--Inline sytle sheet to display H2 and H4 headers in Navy -->
    <STYLE type='text/css'>
     <xsl:call-template           name = "defaultStyle"/>
     <xsl:call-template           name = "qcSummaryStyle"/>
     <xsl:call-template           name = "rsInsertionDeletionStyle"/>
    </STYLE>
   </HEAD>
   <BASEFONT                      FACE = 'Arial, Helvetica, sans-serif'/>
   <xsl:apply-templates         select = "MiscellaneousDocument"/>
  </HTML>
 </xsl:template>


 <!--
 ========================================================================
 Generate the HTML Miscellaneous Document report"
 ======================================================================== -->
 <xsl:template                   match = "MiscellaneousDocument">
  <BODY>

  <!--
  ==================================================================
  Display Report heading and subheading
  ================================================================== -->
   <b>
    <font size='4'>
     <center>
      <xsl:text>Miscellaneous Document</xsl:text>
      <br/>
      <xsl:text>QC Report</xsl:text>
     </center>
    </font>
    <font size='3'>
     <center>
      <xsl:value-of             select = "translate(substring(
                                            document('cdrutil:/date'), 1, 16),
                                            'T', ' ')"/>
     </center>
    </font>
   </b>
   <p/>

   <b>
    <font size='4'>
    <xsl:value-of               select = "concat('CDR',
                                           number(
                                           substring-after($docID, 'CDR')))"/>
    </font>
   </b>
   <p/>

   <xsl:apply-templates/>
  </BODY>
 </xsl:template>


 <!--
 =================================================================
              Display the Document Title
 ================================================================== -->
 <xsl:template                   match = "MiscellaneousDocumentTitle">
  <table width='90%' cellspacing = '0' cellpadding = '0'>
   <tr>
    <th align='left' width='40%'>
     <b>
      <font size='4'>Title</font>
     </b>
    </th>
    <td width='60%'>
     <xsl:apply-templates/>
    </td>
   </tr>
  </table>
 </xsl:template>


 <!--
 =================================================================
                               Display MetaData
 ================================================================= -->
 <xsl:template                   match = "MiscellaneousDocumentMetadata">
  <xsl:element                    name = "h2">
   <xsl:text>Metadata</xsl:text>
  </xsl:element>
  <table width = '90%' cellpadding='0' cellspacing='0'>
   <tr>
    <td width='5%'>
    </td>
    <td width='35%'>
     <b>Type</b>
    </td>
    <td width ='60%'>
     <xsl:apply-templates       select = "MiscellaneousDocumentType"/>
    </td>
   </tr>
   <tr>
    <td> </td>
    <td>
     <b>Language</b>
    </td>
    <td>
     <xsl:apply-templates       select = "Language"/>
    </td>
   </tr>
   <tr>
    <td> </td>
    <td>
     <b>Audience</b>
    </td>
    <td>
     <xsl:apply-templates       select = "Audience"/>
    </td>
   </tr>
  </table>
  <p/>
 </xsl:template>


 <!--
 ==================================================================
 Display Paragraph text
 ================================================================== -->
 <xsl:template                   match = "MiscellaneousDocumentText">
  <xsl:element                    name = "h2">
   <xsl:text>Text</xsl:text>
  </xsl:element>

  <xsl:apply-templates/>
 </xsl:template>


 <!--
 ==================================================================
 ================================================================== -->
 <xsl:template                   match = "Title">
  <b>
   <xsl:value-of                select = "."/>
  </b>
  <p/>
 </xsl:template>


 <!--
 ==================================================================
 Display the name attribute for the PlaceHolder element.
 ================================================================== -->
 <xsl:template                   match = "PlaceHolder">
  <xsl:text>[</xsl:text>
  <xsl:element                    name = "span">
   <xsl:attribute                 name = "class">
    <xsl:text>place-holder-md</xsl:text>
   </xsl:attribute>
   <xsl:value-of                select = "@name"/>
  </xsl:element>
  <xsl:text>]</xsl:text>
 </xsl:template>


 <!--
 ==================================================================
 Display the name attribute for the PlaceHolder element.
 ================================================================== -->
 <xsl:template                   match = "GlossaryTermRef">
  <xsl:element                    name = "a">
   <xsl:attribute                 name = "href">
    <xsl:value-of               select = "concat('/cgi-bin/cdr/Filter.py?',
                                                 'DocId=', @cdr:href,
                                                 '&amp;Filter=set:QC+',
                                                 'GlossaryTermName+',
                                                 'with+Concept+Set')"/>
   </xsl:attribute>
   <xsl:attribute                 name = "target">
    <xsl:text>_blank</xsl:text>
   </xsl:attribute>

   <xsl:value-of                select = "."/>
  </xsl:element>
 </xsl:template>


 <!--
 ==================================================================
 Display the LOERef link
 ================================================================== -->
 <xsl:template                   match = "LOERef">
  <xsl:element                    name = "a">
   <xsl:attribute                 name = "href">
    <xsl:value-of               select = "concat('/cgi-bin/cdr/QcReport.py?',
                                                 'DocId=', @cdr:href,
                                                 '&amp;DocVersion=-1',
                                                 '&amp;Session=guest')"/>
   </xsl:attribute>
   <xsl:attribute                 name = "target">
    <xsl:text>_blank</xsl:text>
   </xsl:attribute>

   <xsl:value-of                select = "."/>
  </xsl:element>
 </xsl:template>


 <!--
 ==================================================================
 Display DateLastModified
 ================================================================== -->
 <xsl:template                   match = "DateLastModified">
  <br />
  <table width='90%' cellpadding = "0" cellspacing = "0">
   <tr>
    <td width='40%'>
     <b>
      <font size='4'>Date Last Modified</font>
     </b>
    </td>
    <td width='60%'>
     <xsl:apply-templates/>
    </td>
   </tr>
  </table>
 </xsl:template>


 <!--
 ==================================================================
 Display Citation
 Misc. Documents are included within Summary documents and the
 Citation numbering/sorting/deduping is done as part of the Summary
 filters.  However, the users requested citations to be displayed
 "just like in a summary" with a sequence number in brackets, for
 example: [5]
 Only minimal insertion/deletion markup (approved) is applied.
 ================================================================== -->
 <xsl:template                   match = "Citation">
  <xsl:element name = "strong">
  <xsl:choose>
   <xsl:when                      test = "@InsertionOrDeletion = 'Insertion'">
    <xsl:element                  name = "span">
     <xsl:attribute               name = "class">
      <xsl:text>insertapproved</xsl:text>
     </xsl:attribute>
     <xsl:text>[</xsl:text>
     <xsl:number                 level = "any"/>
     <xsl:text>]</xsl:text>
    </xsl:element>
   </xsl:when>
   <xsl:when                      test = "@InsertionOrDeletion = 'Deletion'">
    <xsl:element                  name = "span">
     <xsl:attribute               name = "class">
      <xsl:text>deleteapproved</xsl:text>
     </xsl:attribute>
     <xsl:text>[</xsl:text>
     <xsl:number                 level = "any"/>
     <xsl:text>]</xsl:text>
    </xsl:element>
   </xsl:when>
   <xsl:otherwise>
    <xsl:text>[</xsl:text>
     <xsl:number                 level = "any"/>
    <xsl:text>]</xsl:text>
   </xsl:otherwise>
  </xsl:choose>
  </xsl:element>
 </xsl:template>


 <!--
 ==================================================================
 Display placeholder for TableNumber, ReferencedTableNumber
 ================================================================== -->
 <xsl:template                   match = "TableNumber |
                                          FigureNumber |
                                          ReferencedTableNumber |
                                          ReferencedFigureNumber">
  <xsl:text>[#]</xsl:text>
 </xsl:template>


 <!--
 =======================================================================
 Just copy most nodes.
 ======================================================================= -->
 <xsl:template                   match = "@*|node()|comment()|
                                         processing-instruction()">
  <xsl:copy>
   <xsl:apply-templates         select = "@*"/>
   <xsl:apply-templates/>
  </xsl:copy>
 </xsl:template>
</xsl:transform>
