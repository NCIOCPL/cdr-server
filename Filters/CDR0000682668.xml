<?xml version='1.0' encoding='utf-8'?>
<!-- $Id$ -->
<!-- Filter title: Denormalization Filter: Summary Add Board Member - Spanish -->
<!--
 ================================================================
$Id: CDR0000335424.xml 9319 2009-10-28 15:39:13Z bkline $

Filter to denormalize summary data.
   Written by Volker Englisch, 2010-07-28

BZIssue::4836 - Create new section at end of HP summaries

 ================================================================ -->
<xsl:transform               xmlns:xsl = 'http://www.w3.org/1999/XSL/Transform'
                             xmlns:cdr = 'cips.nci.nih.gov/cdr'
                               version = '1.0'>

 <xsl:output                    method = 'xml'
                              encoding = 'utf-8'/>

 <xsl:variable                     name = "sumLanguage">
  <xsl:choose>
   <xsl:when                       test = "/Summary
                                           /SummaryMetaData
                                           /SummaryLanguage = 'English'">
    <xsl:text>EN</xsl:text>
   </xsl:when>
   <xsl:otherwise>
    <xsl:text>ES</xsl:text>
   </xsl:otherwise>
  </xsl:choose>
 </xsl:variable>

  <xsl:variable                   name = "sumId">
   <xsl:value-of                select = "document('cdrutil:/docid')"/>
  </xsl:variable>

  <xsl:variable                   name = "sumNormId"
                                select = "number(
                                           substring-before(
                                           concat(
                                           substring-after($sumId, 'CDR'),
                                                               '#'), '#'))"/>
  <xsl:variable                   name = "sumENId"
                                select = "/Summary
                                          /TranslationOf
                                          /@cdr:ref"/>

  <xsl:variable                   name = "sumENNormId"
                                select = "number(
                                           substring-before(
                                           concat(
                                           substring-after(/Summary
                                                           /TranslationOf
                                                           /@cdr:ref, 'CDR'),
                                                               '#'), '#'))"/>

 <!-- 
 ==================================================================
 Template to add the names of the board members from the English
 board to the Spanish summary.
 ================================================================== -->
 <xsl:template                   match = "Board">
  <xsl:copy-of                  select = "."/>
  <xsl:if                         test = "$sumLanguage = 'ES'">
   <xsl:variable                  name = "BoardInfo"
                                select = "document(concat('cdr:',
                                                           $sumENId,
                                                           '/lastp'))"/>


   <xsl:for-each                select = "$BoardInfo
                                            /Summary
                                            /SummaryMetaData
                                            /PDQBoard[not(contains(Board, 
                                                               'Advisory'))]">
    <xsl:copy-of                select = "BoardMember"/>
   </xsl:for-each>
  </xsl:if>
 </xsl:template>


 <!-- 
 ==================================================================
 Template rule used to do XML to XML transformations which
 copies any attribute node, or node that is child of of something 
 ================================================================== -->
 <xsl:template                   match = '@*|node()'>
  <xsl:copy>
   <xsl:apply-templates         select = '@*|node()'/>
  </xsl:copy>
 </xsl:template>

</xsl:transform>
