<?xml      version = "1.0"?>
<!-- Filter title: Module: InScopeProtocol HP Record Information -->
<!--
 ================================================================
     $Id$

     Filter to process Protocol Record Information
        Written by Volker Englisch, 2004-08-01

     $Log: not supported by cvs2svn $
 ================================================================ -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr">

 <!-- 
 ===============================================================
 Display DateLastModified
 =============================================================== --> 
 <xsl:template                   match = "DateLastModified">
  <hr />
  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <span class="big">Date Last Modified</span>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "."/>
    </td>
   </tr>
  </table>
  <p/>
 </xsl:template>


 <!-- 
 ===============================================================
 Display Related Documents Information
 =============================================================== --> 
 <xsl:template                   match = "RelatedDocuments">
  <span class="big">Related Documents</span>
  <br />
  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Supp Info Link</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:for-each              select = "SupplementaryInfoLink">
      <xsl:value-of             select = "."/>

      <xsl:variable               name = "cdrId"
                                select = "concat('CDR', 
                                           number(
                                            substring-after(
                                             substring-before(
                                              concat(@cdr:ref, '#'), '#'), 
                                                                   'CDR')))"/>
      <xsl:text> (</xsl:text>
       <xsl:value-of           select = "$cdrId"/>
      <xsl:text>)</xsl:text>
      <br/>
     </xsl:for-each>
    </td>
   </tr>
   <xsl:if                        test = "WarehouseBoxNumber">
    <tr>
     <td        xsl:use-attribute-sets = "cell1of2">
      <b>Warehouse Bx No</b>
     </td>
     <td        xsl:use-attribute-sets = "cell2of2">
      <xsl:value-of             select = "WarehouseBoxNumber"/>
     </td>
    </tr>
   </xsl:if>
  </table>
  <p/>
 </xsl:template>


 <!-- 
 =====================================================================
 Display Record Information Date created, Modified, and User
 ===================================================================== --> 
 <xsl:template                   match = "CdrDocCtl">
  <span class="big">Record Information</span>
  <br />
  <table        xsl:use-attribute-sets = "table">
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Created By</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "Create/User"/>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Date</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "substring-before 
                                                 (Create/Date,'T')"/>
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Updated By</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "Modify/User"/><br />
    </td>
   </tr>
   <tr>
    <td         xsl:use-attribute-sets = "cell1of2">
     <b>Date</b>
    </td>
    <td         xsl:use-attribute-sets = "cell2of2">
     <xsl:value-of              select = "substring-before
                                                  (Modify/Date,'T')"/>
    </td>
   </tr>
  </table>
 </xsl:template>
</xsl:transform>
