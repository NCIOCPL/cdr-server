<?xml version='1.0' encoding='UTF-8'?>
<!-- Filter title: Insert External Sites -->

<xsl:transform                version = '1.0'
                            xmlns:xsl = 'http://www.w3.org/1999/XSL/Transform'
                            xmlns:cdr = 'cips.nci.nih.gov/cdr'>

 <xsl:output                   method = 'xml'/>

 <!--
 ====================================================================
 Global parameters.
 ==================================================================== -->
 <xsl:param                   name = "source"/>
 <xsl:param                   name = "lastModified"/>
 <xsl:param                   name = "status"/>
 <xsl:param                   name = "user"/>

 <!--
 =======================================================================
 Copy most things straight through.
 ======================================================================= -->
 <xsl:template                  match = '@*|node()|comment()|
                                         processing-instruction()'>
  <xsl:copy>
   <xsl:apply-templates        select = '@*|node()|comment()|
                                         processing-instruction()'/>
  </xsl:copy>
 </xsl:template>

 <!--
 ======================================================================
 Add place holder for new ExternalSites element.
 ====================================================================== -->
 <xsl:template                 match = "ProtocolAdminInfo">
  <xsl:copy>
   <xsl:apply-templates       select = "@*|StartDate|CurrentProtocolStatus|
                                        ProtocolLeadOrg|CompletionDate|
                                        ExternalSites[not(@Source=$source)]"/>
    <xsl:value-of             select = "'@@EXTERNALSITES@@'"/>
   <xsl:apply-templates       select = "DatedAction"/>
  </xsl:copy>
 </xsl:template>

 <!--
 ======================================================================
 Request #2655: adjust the lead org's protocol status where appropriate.
 ====================================================================== -->
 <xsl:template                 match = "LeadOrgProtocolStatuses/
                                        CurrentOrgStatus">
  <xsl:choose>
   <xsl:when                    test = "../../UpdateMode[@MailerType=
                                        'Protocol_SandP']=$source
                                        and not(StatusName=$status)
                                        and not($status='')">
    <CurrentOrgStatus>
     <StatusName>
      <xsl:value-of           select = "$status"/>
     </StatusName>
     <StatusDate>
      <xsl:value-of           select = "$lastModified"/>
     </StatusDate>
     <Comment>Status adjusted by external S and P import program.</Comment>
     <EnteredBy>
      <xsl:value-of           select = "$user"/>
     </EnteredBy>
     <EntryDate>
      <xsl:value-of           select = "$lastModified"/>
     </EntryDate>
    </CurrentOrgStatus>
    <PreviousOrgStatus>
     <xsl:apply-templates     select = '@*|node()|comment()|
                                         processing-instruction()'/>
    </PreviousOrgStatus>
   </xsl:when>
   <xsl:otherwise>
    <xsl:copy>
     <xsl:apply-templates     select = '@*|node()|comment()|
                                         processing-instruction()'/>
    </xsl:copy>
   </xsl:otherwise>
  </xsl:choose>
 </xsl:template>

 <!--
 ======================================================================
 Request #1800: update DateLastModified value if present.
 ====================================================================== -->
 <xsl:template                 match = "DateLastModified">
  <DateLastModified>
   <xsl:value-of              select = "$lastModified"/>
  </DateLastModified>
 </xsl:template>

 <!--
 ======================================================================
 Strip protocol site children of RSS lead orgs.
 ====================================================================== -->
 <xsl:template                 match = "ProtocolSites">
  <xsl:if                       test = "not(../UpdateMode[@MailerType=
                                        'Protocol_SandP']=$source)">
   <xsl:copy>
    <xsl:apply-templates      select = '@*|node()|comment()|
                                         processing-instruction()'/>
   </xsl:copy>
  </xsl:if>
 </xsl:template>

</xsl:transform>
