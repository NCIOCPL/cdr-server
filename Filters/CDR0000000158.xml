<?xml version="1.0" ?>
<!-- Filter title: Patient Summary QC Report Filter -->
<!--
================================================================
Filter to display Patient Summary QC Reports
Written by Volker Englisch, 2004-07-16

BZIssue::4919 - [Summary] QC Report Bugs
JIRA::OCECDR-4000 - Fix mixed-content problem

================================================================ -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr">

 <xsl:output                  method = "html"
                              indent = "no"/>

 <xsl:include   href = "cdr:name:Module: QC Filter Templates - Summary"/>
 <xsl:include   href = "cdr:name:Module: InLine Markup Formatter"/>
 <xsl:include   href = "cdr:name:Module: Checkbox Formatter"/>
 <xsl:include   href = "cdr:name:Module: Table Formatter"/>
 <xsl:include   href = "cdr:name:Module: STYLE Default"/>
 <xsl:include   href = "cdr:name:Module: STYLE QC Summary"/>
 <xsl:include   href = "cdr:name:Module: STYLE RS Insertion Deletion"/>
 <xsl:include   href = "cdr:name:Module: SCRIPT CdrJQuery"/>

 <xsl:variable                    name = "docID"
                                select = "document('cdrutil:/docid')"/>
 <xsl:variable                    name = "whoAmI"
                                select = "'SummaryPatRS'"/>
 <!--
 ===================================================================
 Main template to create HTML output
 =================================================================== -->
 <xsl:template                   match = "/">
  <xsl:text    disable-output-escaping = "yes">&lt;!DOCTYPE html&gt;</xsl:text>
  <xsl:text>&#xA;</xsl:text>
  <HTML>
   <HEAD>
    <SCRIPT type="text/javascript"
             src="https://code.jquery.com/jquery-3.6.0.min.js">
    </SCRIPT>

    <xsl:variable                 name = "cdrdocs"
                                select = "document('cdrx:/*/CdrCtl')"/>

    <TITLE>
     <xsl:value-of              select = "concat('CDR',
                                           number(
                                           substring-after($docID, 'CDR')))"/>
     <xsl:text>: </xsl:text>
     <xsl:value-of              select = "substring-before(
                                           concat($cdrdocs/CdrDocCtl/
                                                  DocTitle, ';'), ';')"/>
    </TITLE>

    <STYLE type='text/css'>
     <xsl:call-template           name = "defaultStyle"/>
     <xsl:call-template           name = "qcSummaryStyle"/>
     <xsl:call-template           name = "rsInsertionDeletionStyle"/>
    </STYLE>
   </HEAD>

   <BASEFONT                      FACE = 'Arial, Helvetica, sans-serif'/>
   <xsl:apply-templates         select = "Summary"/>
  </HTML>
 </xsl:template>

 <!--
 =====================================================================
 Produce the HTML BODY
 ===================================================================== -->
 <xsl:template                   match = "Summary">
  <!--
  Do only run this report if this is a newly reformatted patient
  summary.
  ==================================================================== -->
  <xsl:choose>
   <xsl:when                      test = "//SummaryAudience='Patients'">
    <BODY>
     <xsl:apply-templates       select = "SummaryTitle"
                                  mode = "TOC"/>

     <!--
     ===========================================================
     Display summary type and audience
     =========================================================== -->
     <xsl:call-template           name = "displayMetaData"/>

     <!--
     ==================================================================
     Display the table of content (TOC)

     The TOC on the QC report is presented as an "On This Page"
     section on Cancer.gov.  All summaries, including SVPC, display
     this section by default. The @SuppressOnThisPageSection attribute
     will turn it off.
     ================================================================== -->
     <xsl:if                      test = "not(/Summary/@SuppressOnThisPageSection='Yes')">
      <xsl:call-template          name = "displayTOC"/>
     </xsl:if>

     <xsl:apply-templates/>

    </BODY>
   </xsl:when>
   <xsl:otherwise>
    <xsl:element                  name = "body">
     <h2>ERROR:</h2>
     <h3>Patient Redline/Strikeout QC Report not valid for HP summaries</h3>
    </xsl:element>
   </xsl:otherwise>
  </xsl:choose>
 </xsl:template>


 <!--
 ==============================================================
 Display a Glossary Link
 ============================================================== -->
 <xsl:template                   match = "GlossaryTermRef |
                                          LOERef">
  <xsl:if                        test  = "/Summary/
                                           SummaryMetaData/
                                           SummaryAudience = 'Patients'">
   <xsl:apply-templates         select = "."
                                  mode = "PE"/>
  </xsl:if>
 </xsl:template>


 <!--
 ================================================================
 Display a Note
 ================================================================ -->
 <xsl:template                   match = "Note">
  <xsl:apply-templates          select = "."
                                  mode = "RS"/>
 </xsl:template>

 <!--
 ================================================================
 Display a  Summary Section Title
 ================================================================ -->
 <xsl:template                   match = "Title">
  <xsl:apply-templates          select = "."
                                  mode = "PT"/>
 </xsl:template>
</xsl:transform>
