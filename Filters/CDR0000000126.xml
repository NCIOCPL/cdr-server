<CdrDoc Type='Filter' Id='CDR0000000126'>
<CdrDocCtl>
<DocValStatus readonly="yes">U</DocValStatus>
<DocActiveStatus readonly="yes">A</DocActiveStatus>
<DocTitle readonly="yes">Citation text and comment</DocTitle>
</CdrDocCtl>
<CdrDocXml><![CDATA[<?xml version = "1.0"?>
<xsl:transform          xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                          version = "1.0"
                        xmlns:cdr = "cips.nci.nih.gov/cdr">

 <!-- Used by unverified citations report. -->
 <xsl:output               method = "xml"/>

 <xsl:template              match = "/">
  <Citation>
   <Text>
    <xsl:apply-templates   select = "Citation/PDQCitation"/>
    <xsl:apply-templates   select = "Citation/PubmedArticle"/>
    <xsl:text>.</xsl:text>
   </Text>
   <Comment>
    <xsl:value-of          select = "Citation/Comment"/>
   </Comment>
  </Citation>
 </xsl:template>
 
 <!-- According to the specs for this report, we shouldn't get any
      of these. -->
 <xsl:template              match = "PubmedArticle">
  <xsl:text>SPECS FOR THIS REPORT DO NOT CALL FOR PUBMED CITATIONS</xsl:text>
 </xsl:template>

 <!-- Format PDQ citation, if that's what we got. -->
 <xsl:template              match = "PDQCitation">
  <xsl:apply-templates     select = "AuthorList"/>
  <xsl:value-of            select = "concat(CitationTitle, '.')"/>
  <xsl:apply-templates     select = "PublicationDetails"/>
 </xsl:template>

 <xsl:template              match = "AuthorList">
  <xsl:for-each            select = "Author">
   <xsl:apply-templates    select = "CollectiveName"/>
   <xsl:apply-templates    select = "LastName"/>
   <xsl:apply-templates    select = "ForeName"/>
   <xsl:apply-templates    select = "FirstName"/>
   <xsl:apply-templates    select = "MiddleName"/>
   <xsl:apply-templates    select = "Initials"/>
   <xsl:if                   test = "position()!=last()">
    <xsl:text>, </xsl:text>
   </xsl:if>
  </xsl:for-each>
  <xsl:if                    test = "@Complete='No'">
   <xsl:text> et al.</xsl:text>
  </xsl:if>
  <xsl:text>: </xsl:text>
 </xsl:template>

 <xsl:template              match = "CollectiveName|LastName">
  <xsl:value-of            select = "."/>
 </xsl:template>

 <xsl:template              match = "ForeName|FirstName|MiddleName">
  <xsl:text> </xsl:text>
  <xsl:value-of            select = "."/>
 </xsl:template>

 <xsl:template              match = "Initials">
  <xsl:text>, </xsl:text>
  <xsl:value-of            select = "."/>
 </xsl:template>

 <xsl:template              match = "PublicationDetails">
  <xsl:text>  </xsl:text>
  <xsl:apply-templates     select = "PublishedIn"/>
  <xsl:value-of            select = "OtherPublicationInformation"/>
 </xsl:template>

 <xsl:template              match = "PublishedIn">
  <xsl:variable              name = "pubIn"
                           select = "document(concat('cdrx:', @cdr:ref))"/>
  <xsl:variable              name = "pdqCite"
                           select = "$pubIn/Citation/PDQCitation"/>
  <xsl:if                    test = "$pdqCite/CitationType = 'Book' or
                                     $pdqCite/CitationType = 'Journal'">
   <xsl:variable             name = "citeTitle"
                           select = "$pdqCite/CitationTitle"/>
   <xsl:variable             name = "pubDetails"
                           select = "$pdqCite/PublicationDetails"/>
   <xsl:variable             name = "other"
                           select = "$pubDetails/OtherPublicationInformation"/>
   <xsl:text>In: </xsl:text>
   <xsl:choose>
    <xsl:when                test = "$pdqCite/CitationType = 'Book'">
     <xsl:apply-templates  select = "$pdqCite/AuthorList"/>
     <xsl:value-of         select = "concat($citeTitle, '. ', $other, ', ')"/>
    </xsl:when>
    <xsl:otherwise>
     <xsl:value-of         select = "concat($citeTitle, ', ')"/>
    </xsl:otherwise>
   </xsl:choose>
  </xsl:if>
 </xsl:template>

</xsl:transform>]]>
</CdrDocXml>
</CdrDoc>