<?xml version="1.0"?>
<!-- Creator:  Volker Englisch
     Created:  11/07/2002        
     Comment:  This filter is the second part of a two part filtering
               process.  The first part (Vendor Filter: InScopeProtocol -
               Sort ProtocolSite) is sorting ProtocolSite and ProtPerson
               Elements and combining the ProtPerson fragments from
               identical ProtocolSite fragments.
               This second part ensures that each ProtocolSite/ProtPerson
               combination only 
               appears once in the resulting output. -->

<!-- 
 ==================================================================
 $Id$

 $Log: not supported by cvs2svn $
 ================================================================== -->
<xsl:transform              xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                            xmlns:cdr = "cips.nci.nih.gov/cdr"
              exclude-result-prefixes = "cdr"
                              version = "1.0">

 <xsl:output                   method = "xml" 
                 omit-xml-declaration = "no"
                             encoding = "UTF-8"/>


 <!--
 ====================================================================
 Root element.
 ==================================================================== -->
 <xsl:template               match = "/">
  <xsl:apply-templates/>
 </xsl:template>


 <!--
 ================================================================
 Default rule, copying back out everything just as we got it.
 ================================================================ -->
 <xsl:template                  match = "@*|comment()|*|
                                         processing-instruction()|text()">
  <xsl:copy>
   <xsl:apply-templates        select = "@*|comment()|*|
                                         processing-instruction()|text()"/>
  </xsl:copy>
 </xsl:template>


 <!--  
 ===================================================================
 Template to remove duplicate, identical ProtocolSite elements   
 =================================================================== -->
 <xsl:template                  match = "ProtocolSites">
  <xsl:copy>
   <xsl:apply-templates        select = "ProtocolSite
                                         [not(.=preceding::ProtocolSite)]"/>
  </xsl:copy>
 </xsl:template>

 <!--  
 ===================================================================
 Template to remove duplicate, identical ProtPerson elements   
 =================================================================== -->
 <xsl:template                  match = "ProtocolSite/ProtPerson">
  <xsl:if                        test = "not(.=preceding-sibling::ProtPerson)">
   <xsl:copy-of                select = "."/>
  </xsl:if>
 </xsl:template>

</xsl:transform>
