<?xml      version = "1.0"?>
<!-- Filter title: Denormalization Filter: Secondary Links -->
<!--
 ================================================================

 Filter to denormalize SummaryRef elements within MiscDoc
 Fragments.

 Note: Do not include this filter as a member of the
       "Denormalization Summary Set" since it's only necessary
       to do this secondary denormalization in order to create
       these secondary SummaryRef links in the QC reports.

    Written by Volker Englisch, 2003-08-12

 ================================================================ -->
<xsl:transform                xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                                version = "1.0"
                              xmlns:cdr = "cips.nci.nih.gov/cdr">

 <xsl:output                     method = "xml"
                   omit-xml-declaration = "no"/>

 <!--
 =================================================================
 Copy all nodes as is (except for those specified below)
 ================================================================= -->
 <xsl:template                    match = "Summary">
  <xsl:copy>
   <xsl:apply-templates/>
  </xsl:copy>
 </xsl:template>


 <!--
 =================================================================
 Template to denormalize the SummaryRef elements within the
 MiscDocLink fragments.
 We need this denormalization in order to correctly create the
 link in the QC report to the listed summaries.
 ================================================================= -->
 <xsl:template                    match = "MiscellaneousDocLink//SummaryRef">
  <xsl:variable                    name = "SummaryRefID"
                                 select = "@cdr:href"/>
  <xsl:variable                    name = "SummaryRefInfo"
                                 select = "document(concat('cdr:',$SummaryRefID,
                                                           '/lastp'))"/>
  <xsl:element                     name = "SummaryRef">
   <xsl:attribute                  name = "cdr:href">
    <xsl:value-of                select = "@cdr:href"/>
   </xsl:attribute>

   <SummaryRefText>
    <xsl:value-of                select = "."/>
   </SummaryRefText>

   <Summary>
    <LinkedSummaryType>
     <xsl:value-of               select = "$SummaryRefInfo/Summary
                                             /SummaryMetaData
                                             /SummaryType"/>
    </LinkedSummaryType>
    <LinkedSummaryAudience>
     <xsl:value-of               select = "$SummaryRefInfo/Summary
                                             /SummaryMetaData
                                             /SummaryAudience"/>
    </LinkedSummaryAudience>
    <xsl:if                        test = "$SummaryRefInfo/Summary
                                             //KeyPoint">
     <LinkedKeyPoints>
      <xsl:value-of              select = "count($SummaryRefInfo/Summary
                                             //KeyPoint)"/>
     </LinkedKeyPoints>
    </xsl:if>
   </Summary>
  </xsl:element>
 </xsl:template>


 <!--
 ==================================================================
 Template rule used to do XML to XML transformations which
 copies any attribute node, or node that is child of of something
 ================================================================== -->
 <xsl:template                    match = "@*|node()">
  <xsl:copy>
   <xsl:apply-templates          select = "@*|node()"/>
  </xsl:copy>
 </xsl:template>

</xsl:transform>
