<CdrDoc Type='Filter' Id='CDR0000000153'>
<CdrDocCtl>
<DocValStatus readonly="yes">U</DocValStatus>
<DocActiveStatus readonly="yes">A</DocActiveStatus>
<DocTitle readonly="yes">Health Professional Summary Report-Bold/Underline</DocTitle>
<DocComment>CVS-V1.33: Proposed deletion</DocComment><ReadyForReview readonly="yes">N</ReadyForReview>
</CdrDocCtl>
<CdrDocXml><![CDATA[<?xml version="1.0"?>
<!-- 
===================================================================
Filter to display revised Summary with Insertions in bold/underline
(Based on new Denormalization)
=================================================================== -->
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr">

 <xsl:output                    method = "html" 
                                indent = "no"/>

 <xsl:include   href = "cdr:name:InLine Markup Formatter"/>
 <xsl:include   href = "cdr:name:Module: Table Formatter"/>
 <xsl:include   href = "cdr:name:Module: STYLE Default"/>  

 <!-- 
 ==================================================================
 Create HTML Output
 ================================================================== -->
 <xsl:template                   match = "/">
  <!-- <!DOCTYPE HTML PUBLIC '-//IETF//DTD HTML//EN' /> -->
  <HTML>
   <HEAD>
    <xsl:variable                 name = "cdrdocs" 
                                select = "document('cdrx:/*/CdrCtl')"/>
    <TITLE>
     <xsl:value-of              select = "document('cdrutil:/docid')"/>
     <xsl:text>: </xsl:text>
     <xsl:value-of              select = "substring-before(                
                                          $cdrdocs/CdrDocCtl/DocTitle, ';')"/>
    </TITLE>
     
    <!--Inline sytle sheet to display H2 and H4 headers in Navy -->
    <STYLE>
     <xsl:call-template           name = "defaultStyle"/>  

     h2 { font style: bold font size: 14pt}
     h3 { font style: bold font size: 13pt}
     h4 { font style: bold font size: 12pt}
     td { font size: 8pt}
    </STYLE>
   </HEAD>
   <BASEFONT                      FACE = 'Arial, Helvetica, sans-serif'/> 
   <xsl:apply-templates         select = "Summary"/>
  </HTML>
 </xsl:template>

 <!-- 
 ==================================================================
 Display summary title
 ================================================================== -->
 <xsl:template                   match = "Summary">
  <BODY>
   <xsl:apply-templates         select = "SummaryTitle" 
                                  mode = "TOC"/>  

   <!-- 
   ==================================================================
   Display a Contents List of Summary Section Titles 
   10/06/2002 - Modified code to remove 4th level titles (CB) 
   10/08/2002 - took breaks out at end of titles.
   =================================================================== -->
   <ul Class="lnone">
    <xsl:for-each              select = "SummarySection">
     <xsl:call-template          name = "TocSummarySection">
      <xsl:with-param            name = "level"
                               select = "1"/>
     </xsl:call-template>  
    </xsl:for-each>
   </ul>

   <xsl:apply-templates/>

  </BODY>
 </xsl:template>
          
 <!-- Remove the CdrDocCtl elements from the HTML output -->
 <xsl:template                  match = "CdrDocCtl"/>

 <!-- 
 ======================================================================
 Display the inline Citation as numbered links to the Reference section
 (e.g. [2, 4, 5])
 ===================================================================== -->
 <xsl:template                  match = "Citation">
  <xsl:if                        test = "not(@InsertionOrDeletion='Deletion')">
   <xsl:variable                 name = "nextRef" 
                               select = "position()+1"/>
   <xsl:variable                 name = "firstRef" 
                               select = "position()-1"/>
   <xsl:variable                 name = "ref" 
                               select = "."/>
      
   <!--
   Displaying the Left Bracket of a list of citations with marked up 
   ================================================================= -->
   <xsl:if                       test = "name(../node()[$firstRef])
                                                            != 'Citation'">
    <xsl:choose>
     <xsl:when                   test = "@InsertionOrDeletion = 'Insertion'">
      <b><u>
       <xsl:text>[</xsl:text>
      </u></b>
     </xsl:when>
     <xsl:when                   test = "@InsertionOrDeletion = 'Deletion'">
      <xsl:choose>
       <xsl:when                 test = "@existing">
        <xsl:text>[</xsl:text>
       </xsl:when>
       <xsl:otherwise>
        <strike>
         <xsl:text>[</xsl:text>
        </strike>
       </xsl:otherwise>
      </xsl:choose>
     </xsl:when>
     <xsl:otherwise>
      <xsl:text>[</xsl:text>
     </xsl:otherwise>
    </xsl:choose>
   </xsl:if>
                    
   <!-- 
   Convert the Citation into a number linking to the Reference Section
   =================================================================== -->
   <xsl:choose>
    <xsl:when                    test = "@InsertionOrDeletion = 'Insertion'">
     <xsl:choose>
      <xsl:when                  test = "@RevisionLevel='proposed'">
       <b>
        <xsl:value-of          select = "@refidx"/>
       </b>
      </xsl:when>
      <xsl:otherwise>
       <b><u>
        <xsl:value-of          select = "@refidx"/>
       </u></b>
      </xsl:otherwise>
     </xsl:choose>
    </xsl:when> 
    <xsl:when                    test = "@InsertionOrDeletion = 'Deletion'"/>  
    <xsl:otherwise>
     <xsl:value-of             select = "@refidx"/>
    </xsl:otherwise> 
   </xsl:choose>  	    
	    	
   <!--
   Displaying the comma of a list of citations with marked up 
   ========================================================== -->
   <xsl:if                       test = "name(../node()[$nextRef])= 'Citation'">
    <xsl:choose>
     <xsl:when                   test = "@InsertionOrDeletion = 'Insertion'">
      <b><u>
       <xsl:text>, </xsl:text>
      </u></b>
     </xsl:when>
     <xsl:when                   test = "@InsertionOrDeletion = 'Deletion'">
      <xsl:choose>
       <xsl:when                 test = "@existing">
        <xsl:text>, </xsl:text>
       </xsl:when>
       <xsl:otherwise>
        <strike>
         <xsl:text>, </xsl:text>
        </strike>
       </xsl:otherwise>
      </xsl:choose>
     </xsl:when>
     <xsl:otherwise>
      <xsl:text>, </xsl:text>
     </xsl:otherwise>
    </xsl:choose>
   </xsl:if>
          
   <!--
   Displaying the Right Bracket of a list of citations with marked up 
   ================================================================== -->
   <xsl:if                       test = "name(../node()[$nextRef])!='Citation'">
    <xsl:choose>
     <xsl:when                   test = "@InsertionOrDeletion = 'Insertion'">
      <b><u>
       <xsl:text>]</xsl:text>
      </u></b>
     </xsl:when>
     <xsl:when                   test = "@InsertionOrDeletion = 'Deletion'">
      <xsl:choose>
       <xsl:when                 test = "@existing">
        <xsl:text>]</xsl:text>
       </xsl:when>
       <xsl:otherwise>
        <strike>
         <xsl:text>]</xsl:text>
        </strike>
       </xsl:otherwise>
      </xsl:choose>
     </xsl:when>
     <xsl:otherwise>
      <xsl:text>]</xsl:text>
     </xsl:otherwise>
    </xsl:choose>
   </xsl:if>
  </xsl:if>    
 </xsl:template>
                          
 <!-- 
 ================================================================== 
 Display an Insertion 
 ================================================================== -->
 <xsl:template                 match = "Insertion">
  <xsl:choose>
   <xsl:when                    test = "@RevisionLevel='proposed'">
    <b>
     <xsl:apply-templates/>
    </b>
   </xsl:when>
   <xsl:otherwise> 
    <b><u>
     <xsl:apply-templates/>
    </u></b>
   </xsl:otherwise>
  </xsl:choose>
 </xsl:template>

 <!-- 
 ==================================================================
 Display proposed Deletion with Markup
 ================================================================== -->
 <xsl:template                 match = "Deletion"> 
  <xsl:if                       test = "@RevisionLevel = 'proposed'">
   <strike>
    <xsl:apply-templates/>
   </strike>  
  </xsl:if>
 </xsl:template>      

 <!-- 
 ================================================================
 Display Comments 
 ================================================================ -->
 <!-- xsl:template                  match = "Comment">
  <xsl:apply-templates         select = "." 
                                 mode = "RS"/>  
 </xsl:template -->  


 <!-- 
 ================================================================
 Display a Note
 ================================================================ -->
 <xsl:template                  match = "Note">
  <xsl:apply-templates         select = "." 
                                 mode = "BU"/>  
 </xsl:template>  


 <!-- 
 ==================================================================
 Don't display any Deletion unless it's marked as proposed
 ================================================================== -->
 <xsl:template                 match = "Deletion[@RevisionLevel = 'approved'] |
                                        Deletion[@RevisionLevel = 'publish']"/>

   
   
 <!-- 
 =========================================================================== 
 This template deletes data elements not used in the display 
 =========================================================================== -->
 <xsl:template                 match = "PdqKey           | PDQBoard         |
                                        SectionType      | DocId            |
                                        Diagnosis        | SummaryLanguage  |
                                        SummaryType      | SummaryAudience  | 
                                        SummaryTitle     | MainTopics       |
                                        SecondaryTopics  | ReplacementFor   |
                                        DateLastModified | TranslationOf    |
                                        PatientVersionOf"/>


 <!-- 
 =====================================================================
 Display Reference Section
 ===================================================================== -->
 <xsl:template                 match = "ReferenceList"> 
   <xsl:choose>
    <xsl:when                   test = "//SummaryLanguage='Spanish'">
     <h3>Referencias:</h3>
    </xsl:when>
    <xsl:otherwise>
     <h3>References:</h3>
    </xsl:otherwise>
   </xsl:choose>    
       
   <ol>
    <xsl:for-each             select = "Citation">
     <xsl:call-template         name = "markupCitation"/>
    </xsl:for-each>
   </ol>
 </xsl:template>
     

 <!-- 
 =====================================================================
 Display the Citations in the Reference Section with Markup 
 ===================================================================== -->
 <xsl:template                  name = "markupCitation"> 
  <xsl:choose>
   <xsl:when                    test = "@InsertionOrDeletion = 'Insertion'">
    <p><li>
     <b><u>
      <xsl:value-of           select = "."/>
     </u></b>
    </li></p>
   </xsl:when>
   <xsl:when                    test = "@InsertionOrDeletion = 'Deletion'">
    <xsl:choose>
     <xsl:when                  test = "@existing">
      <p><li>
       <xsl:value-of          select = "."/>
      </li></p>
     </xsl:when>

     <!-- Deletions are not to be displayed in the Bold/Underline Report -->
     <!-- xsl:otherwise>
      <p><li><strike>
       <xsl:value-of          select = "."/>
      </strike></li></p>
     </xsl:otherwise -->

    </xsl:choose>
   </xsl:when>
   <xsl:otherwise>
    <p><li>
     <xsl:value-of            select = "."/>
    </li></p>
   </xsl:otherwise>
  </xsl:choose>    
 </xsl:template>


 <!-- 
 ==============================================================
 Display a Glossary Link
 ============================================================== -->
 <xsl:template                  match = "GlossaryTermRef | LOERef">
  <xsl:choose>
   <xsl:when                     test = "/Summary/SummaryMetaData/
                                          SummaryAudience = 
                                                          'Health professionals'
                                          and 
                                         /Summary/SummaryMetaData/
                                          SummaryType != 
                                       'Complementary and alternative medicine' 
                                          and
                                         /Summary/SummaryMetaData/
                                          SummaryLanguage = 'English'">
    <xsl:apply-templates       select = "."  
                                 mode = "HPE"/>
   </xsl:when>
   <xsl:when                     test = "/Summary/SummaryMetaData/
                                          SummaryType = 
                                     'Complementary and alternative medicine'">
    <xsl:apply-templates       select = "."  
                                 mode = "PE"/>
   </xsl:when>
   <xsl:when                     test = "/Summary/
                                         SummaryMetaData/SummaryAudience =
                                         'Health professionals' 
                                         and 
                                        /Summary/SummaryMetaData/
                                         SummaryLanguage = 'Spanish'">
    <xsl:apply-templates       select = "."  
                                 mode = "HPS"/>
   </xsl:when>
   <xsl:otherwise>
    <xsl:apply-templates/>
   </xsl:otherwise>
  </xsl:choose> 
 </xsl:template>
</xsl:transform>
]]>
</CdrDocXml>
</CdrDoc>