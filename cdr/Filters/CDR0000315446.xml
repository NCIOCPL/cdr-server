<CdrDoc Type='Filter' Id='CDR0000315446'>
<CdrDocCtl>
<DocValStatus readonly="yes">U</DocValStatus>
<DocActiveStatus readonly="yes">A</DocActiveStatus>
<DocTitle readonly="yes">Module: PersonLocations Denormalization</DocTitle>
<DocComment>CVS-V1.9: Fix Clinical Trials Office (Bug 1187)</DocComment><ReadyForReview readonly="yes">N</ReadyForReview>
</CdrDocCtl>
<CdrDocXml><![CDATA[<?xml      version = "1.0"?>
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
                               version = "1.0">

 <xsl:output                    method = "xml" 
                  omit-xml-declaration = "no"
                              encoding = "UTF-8"/>

 <!--
 ===================================================================
 Denormalize the Person information.
 Note:  Can not extract the call for the call-template out of the 
        when-otherwise clauses since the contactID variable would be
        out-of-scope.
 =================================================================== -->
 <xsl:template                     match = "Person           |
                                            SpecificPerson   |  
                                            CIPSContactPerson|
                                            BoardMemberName  |
                                            ExternalSitePIID">
  <xsl:element                      name = "{name()}">
   <Person>
    <xsl:choose>
     <xsl:when                      test = "name() = 'SpecificPerson'">
      <xsl:for-each               select = "Person/@*">
       <xsl:attribute               name = "{name()}">
        <xsl:value-of             select = "."/>
       </xsl:attribute>
      </xsl:for-each>
      <xsl:variable                 name = "contactID"
                                  select = "Person/@cdr:ref"/>
      <xsl:variable                 name = "contactPerson"
                                  select = "document(concat('cdr:', $contactID, 
                                                          '/last'))"/>
      <xsl:call-template            name = "getPerson">
       <xsl:with-param              name = "contactPerson"
                                  select = "$contactPerson"/>
      </xsl:call-template>
     </xsl:when>
     <xsl:otherwise>
      <xsl:for-each               select = "@*">
       <xsl:attribute               name = "{name()}">
        <xsl:value-of             select = "."/>
       </xsl:attribute>
      </xsl:for-each>
      <xsl:variable                 name = "contactID"
                                  select = "@cdr:ref"/>
      <xsl:variable                 name = "contactPerson"
                                  select = "document(concat('cdr:', $contactID, 
                                                          '/last'))"/>
      <xsl:call-template            name = "getPerson">
       <xsl:with-param              name = "contactPerson"
                                  select = "$contactPerson"/>
      </xsl:call-template>
     </xsl:otherwise>
    </xsl:choose>
   </Person>
   <xsl:copy-of                   select = "Role"/>
   <xsl:copy-of                   select = "SpecificPhone"/>
   <xsl:copy-of                   select = "SpecificEmail"/>
  </xsl:element>
 </xsl:template>


 <!-- 
 ===================================================================
 Denormalize the Organization Information (Parent, CCOP, etc.)
 We need to handle the RSS ExternalSiteOrgID separately because it
 could also refer to a _Person_ document instead of an organization.
 =================================================================== -->
 <xsl:template                   match = "ExternalSiteOrgID      |
                                          ParentOrganization     |
                                          ResearchBaseFor        |
                                          MemberOfAdHocGroup     |
                                          CCOP                   |
                                          CooperativeGroup       |
                                          MainMember             |
                                          OtherPracticeLocation/
                                            OrganizationLocation">
  <xsl:choose>
   <xsl:when                      test = "name() = 'ExternalSiteOrgID'">
    <xsl:variable                 name = "orgorpersonID"
                                select = "@cdr:ref"/>
    <xsl:variable                 name = "nodeOrgOrPerson"
                                select = "document(concat('cdr:', 
                                                           $orgorpersonID, 
                                                          '/last'))"/>
    <xsl:choose>
     <!-- The denormalized document is an organization doc. -->
     <xsl:when                    test = "$nodeOrgOrPerson/Organization">
      <xsl:call-template          name = "getOrganization"/>
     </xsl:when>
     <!-- The denormalized document is a person doc. -->
     <xsl:otherwise>
      <xsl:copy>
       <Person>
        <xsl:for-each           select = "@*">
         <xsl:attribute           name = "{name()}">
          <xsl:value-of         select = "."/>
         </xsl:attribute>
        </xsl:for-each>
        <xsl:variable             name = "contactID"
                                select = "@cdr:ref"/>
        <xsl:variable             name = "contactPerson"
                                select = "document(concat('cdr:', $contactID, 
                                                          '/last'))"/>
        <xsl:call-template        name = "getPerson">
         <xsl:with-param          name = "contactPerson"
                                select = "$contactPerson"/>
        </xsl:call-template>
       </Person>
      </xsl:copy>
     </xsl:otherwise>
    </xsl:choose>
   </xsl:when>
   <xsl:otherwise>
    <xsl:call-template            name = "getOrganization"/>
   </xsl:otherwise>
  </xsl:choose>
 </xsl:template>


 <!--
 ===================================================================
 Denormalize the information for PoliticalSubUnit/State information.
 =================================================================== -->
 <xsl:template                   match = "PoliticalSubUnit_State">
  <xsl:variable                   name = "stateId"
                                select = "@cdr:ref"/>

  <xsl:variable                   name = "state"
                                select = "document(concat('cdr:', $stateId, 
                                                          '/lastp'))"/>
  <xsl:element                    name = "PoliticalSubUnit_State">
   <xsl:attribute                 name = "cdr:ref">
    <xsl:value-of               select = "$stateId"/>
   </xsl:attribute>
   <xsl:copy-of                 select = "$state/PoliticalSubUnit/
                                          PoliticalSubUnitFullName"/>
   <xsl:copy-of                 select = "$state/PoliticalSubUnit/
                                          PoliticalSubUnitShortName"/>
  </xsl:element>
 </xsl:template>


 <!--
 ===================================================================
 Template to correctly handle the Country element in the citations.
 Since a template for the Country documents exists this secondary
 Country template is needed.
 =================================================================== -->
 <xsl:template                   match = "MedlineJournalInfo/Country">
  <xsl:copy>
   <xsl:apply-templates/>
  </xsl:copy>
 </xsl:template>


 <!--
 ===================================================================
 Denormalize the Country information.
 =================================================================== -->
 <xsl:template                   match = "Country">
  <xsl:element                    name = "Country">
   <xsl:variable                  name = "countryId"
                                select = "@cdr:ref"/>
   <xsl:variable                  name = "country"
                                select = "document(concat('cdr:', $countryId, 
                                                          '/lastp'))"/>
   <xsl:attribute                 name = "cdr:ref">
    <xsl:value-of               select = "$countryId"/>
   </xsl:attribute>
   <xsl:copy-of                 select = "$country/Country/CountryFullName"/>
   <xsl:copy-of                 select = "$country/Country/CountryShortName"/>
   <xsl:copy-of                 select = "$country/Country/PostalCodePosition"/>
  </xsl:element>
 </xsl:template>


 <!-- 
 ===================================================================
 Named Templates
 =================================================================== --> 
 <!-- 
 ===================================================================
 Template to detect circular reference in Parent/Child relationship
 =================================================================== -->
 <xsl:template                    name = "getParentOrgRecurse">
  <xsl:param                      name = "orgPID"/>
  <xsl:param                      name = "level"/>
  <xsl:param                      name = "parentIds"/>

  <xsl:variable                   name = "orgParent"
                                select = "document(concat('cdrx:', $orgPID, 
                                                          '/last'))"/>
  <!-- 
  Do nothing or display the parent IDs and terminate if more then 
  10 ancestors were detected
  =================================================================== -->
  <xsl:choose>
   <xsl:when                      test = "$level &lt; 10">
    <xsl:for-each               select = "$orgParent/Organization/
                                          OrganizationParent/
                                          ParentOrganization">

     <xsl:call-template           name = "getParentOrgRecurse">
      <xsl:with-param             name = "orgPID"
                                select = "@cdr:ref"/>
      <xsl:with-param             name = "parentIds"
                                select = "concat($parentIds, ' ', 'CDR', 
                                          number(substring(@cdr:ref, 4)))"/>
      <xsl:with-param             name = "level"
                                select = "$level + 1"/>
     </xsl:call-template>
    </xsl:for-each>
    <!-- xsl:apply-templates        select = "$orgParent/Organization/
                                          OrganizationParent/
                                          ParentOrganization"/ -->
   </xsl:when>
   <xsl:otherwise>
    <xsl:message terminate = "yes">
     <xsl:text>ERROR: Too many parent levels (></xsl:text>
     <xsl:value-of              select = "$level"/>
     <xsl:text>). </xsl:text>
     <xsl:value-of              select = "$parentIds"/>
    </xsl:message>
   </xsl:otherwise>
  </xsl:choose>
 </xsl:template>


 <!-- 
 ===================================================================
 Extract the information for one lead organization.
 =================================================================== -->
 <xsl:template                    name = "getOrganization">
  <xsl:element                    name = "{name()}">
   <xsl:for-each              select = "../@*">
    <xsl:attribute              name = "{name()}">
     <xsl:value-of            select = "."/>
    </xsl:attribute>
   </xsl:for-each>

   <xsl:variable                  name = "orgID"
                                select = "@cdr:ref"/>
   <xsl:variable                  name = "org"
                                select = "document(concat('cdr:', $orgID, 
                                                          '/last'))"/>
   <Organization>
    <xsl:for-each               select = "@*">
     <xsl:attribute               name = "{name()}">
      <xsl:value-of             select = "."/>
     </xsl:attribute>
    </xsl:for-each>

    <xsl:copy-of                select = "$org/Organization/
                                          OrganizationNameInformation"/>

    <!-- 
    Check to ensure we're not creating an infinite loop here
    ============================================================= -->
    <xsl:for-each               select = "$org/Organization/
                                          OrganizationParent/
                                          ParentOrganization[1]">
     <xsl:call-template           name = "getParentOrgRecurse">
      <xsl:with-param             name = "orgPID"
                                select = "@cdr:ref"/>
      <xsl:with-param             name = "level"
                                select = "'1'"/>
      <xsl:with-param             name = "parentIds"
                                select = "concat('CDR', 
                                          number(substring(@cdr:ref, 4)))"/>
     </xsl:call-template>
    </xsl:for-each>

    <!-- 
    If we are still in business no infinite loop has been detected.
    Continue to display the parent/child tree
    =============================================================== -->
    <xsl:apply-templates        select = "$org/Organization/
                                          OrganizationParent/
                                          ParentOrganization"/>

    <!--  
    All we need for the ParentOrganization, CCOP, etc. is the parent 
    name and the information if there exists a grandparent.  
    However, the template is called via OrganizationLocation we need 
    (almost) all available information.
    ===================================================================  -->
    <xsl:if                       test = "name() = 'OrganizationLocation'
                                          or
                                          name() = 'OrgSiteID'
                                          or
                                          name() = 'LeadOrganizationID'
                                          or
                                          name() = 'ClinicalTrialOffice'">
     <xsl:apply-templates       select = "$org/Organization/
                                          OrganizationDetails"/>
     <xsl:apply-templates       select = "$org/Organization/
                                          OrganizationLocations"/>
     <xsl:apply-templates       select = "$org/Organization/
                                          OrganizationAffiliations"/>
     <xsl:copy-of               select = "$org/Organization/
                                          Status"/>
     <xsl:copy-of               select = "$org/Organization/
                                          OrganizationType"/>

     <!-- 
     These 2 elements are listed in the Schema but there doesn't exist
     any data.  Keeping these commented out for the time being.
     ==================================================================   
     <xsl:copy-of               select = "$org/Organization/
                                          ProtocolIDAcronym"/>
     <xsl:copy-of               select = "$org/Organization/
                                          PreferredProtocolOrganization"/> -->
    </xsl:if>

    <!--  
    The RSS information allows the link of the ExternalSiteOrgID to be
    an org or a person link.  We handle this situation as a special case
    ==================================================================== -->
    <xsl:if                       test = "name() = 'ExternalSiteOrgID'">
     <xsl:apply-templates       select = "$org/Organization/
                                          OrganizationLocations"/>

     <!-- 
     These 2 elements are listed in the Schema but there doesn't exist
     any data.  Keeping these commented out for the time being.
     ==================================================================   
     <xsl:copy-of               select = "$org/Organization/
                                          ProtocolIDAcronym"/>
     <xsl:copy-of               select = "$org/Organization/
                                          PreferredProtocolOrganization"/> -->
    </xsl:if>
   </Organization>
   <xsl:if                        test = "name() = 'OrgSiteID'">
    <xsl:apply-templates        select = "../OrgSiteStatus"/>
    <OrgSiteContact>
     <xsl:apply-templates       select = "../OrgSiteContact/SpecificPerson |
                                          ../OrgSiteContact/GenericPerson"/>
     <xsl:for-each              select = "../OrgSiteContact/
                                             ClinicalTrialOffice">
      <xsl:call-template          name = "getOrganization"/>
     </xsl:for-each>
    </OrgSiteContact>
   </xsl:if>
  </xsl:element>
 </xsl:template>


 <!-- 
 ===================================================================
 Extract the information for one PrivatePractice Person.
 =================================================================== -->
 <xsl:template                    name = "getPrivatePracticeSite">
  <!-- Carry over the SuppressContactInformation attribute from the 
       PrivatePracticeSite element -->
  <xsl:element                    name = "{name()}">
   <xsl:for-each                select = "../@*">
    <xsl:attribute                name = "{name()}">
     <xsl:value-of              select = "."/>
    </xsl:attribute>
   </xsl:for-each>

   <xsl:variable                  name = "privorgID"
                                select = "@cdr:ref"/>
   <xsl:variable                  name = "privorg"
                                select = "document(concat('cdr:', $privorgID, 
                                                          '/last'))"/>
   <!-- 
   Adding an additional wrapper element to allow the PrivatePracticePerson 
   in the vendor filter to be formatted with the Person template 
   ======================================================================= -->
   <PrivatePracticePerson>
    <Person>
     <xsl:for-each              select = "@*">
      <xsl:attribute              name = "{name()}">
       <xsl:value-of            select = "."/>
      </xsl:attribute>
     </xsl:for-each>

     <xsl:variable                name = "contactID"
                                select = "@cdr:ref"/>
     <xsl:variable                name = "contactPerson"
                                select = "document(concat('cdr:', $contactID, 
                                                          '/last'))"/>
     <xsl:call-template           name = "getPerson">
      <xsl:with-param             name = "contactPerson"
                                select = "$contactPerson"/>
     </xsl:call-template>
    </Person>
   </PrivatePracticePerson>

   <xsl:copy-of                 select = "../Role"/>
   <xsl:copy-of                 select = "../PrivatePracticeSiteStatus"/>

  </xsl:element>
 </xsl:template>


 <!--
 ===================================================================
 Extract the information for person information.
 =================================================================== -->
 <xsl:template                    name = "getPerson">
  <xsl:param                      name = "contactPerson"/>

  <xsl:copy-of                  select = "$contactPerson/Person/
                                          PersonNameInformation"/>
  <xsl:copy-of                  select = "$contactPerson/Person/
                                          ProfessionalInformation"/>
  <!-- 
  Only limited information is needed for the CIPSContactPerson.
  The denormalization doesn't need to pick up these elements.   
  ============================================================ -->
  <xsl:if                         test = "name() != 'CIPSContactPerson'">
   <xsl:apply-templates         select = "$contactPerson/Person/
                                          PersonLocations"/>
   <xsl:copy-of                 select = "$contactPerson/Person/
                                          Status"/>
  </xsl:if>
 </xsl:template>

</xsl:transform>
]]>
</CdrDocXml>
</CdrDoc>