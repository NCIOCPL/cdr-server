<CdrDoc Type='Filter' Id='CDR0000000160'>
<CdrDocCtl>
<DocValStatus readonly="yes">U</DocValStatus>
<DocActiveStatus readonly="yes">A</DocActiveStatus>
<DocTitle readonly="yes">Summaries Toc Report</DocTitle>
<DocComment>CVS-V1.32: Proposed deletion</DocComment><ReadyForReview readonly="yes">N</ReadyForReview>
</CdrDocCtl>
<CdrDocXml><![CDATA[<?xml version="1.0"?>
<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                               version = "1.0"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
               exclude-result-prefixes = "cdr">

 <xsl:output                    method = "html"/>

 <xsl:include   href = "cdr:name:Module: STYLE Default"/>  
 <xsl:include   href = "cdr:name:Module: STYLE QC Summary"/>  
     
 <xsl:variable                    name = "docID"
                                select = "document('cdrutil:/docid')"/>
 <xsl:param                       name = "showLevel"
                                select = "'3'"/>
 <xsl:param                       name = "showId"
                                select = "'N'"/>
 <!--
 ===================================================================
 Main template
 =================================================================== -->
 <xsl:template                   match = "/">
   <xsl:apply-templates         select = "Summary"/>
 </xsl:template>

 <!-- 
 =====================================================================
 Produce the HTML BODY 
 ===================================================================== -->
 <xsl:template                   match = "Summary">
  <H5>
   <xsl:if                        test = "$showId = 'Y'">
    <xsl:value-of               select = "concat('CDR', 
                                           number(
                                           substring-after($docID, 'CDR')))"/>
    <xsl:text> - </xsl:text>
   </xsl:if>
   <xsl:apply-templates         select = "SummaryTitle" 
                                  mode = "TOC"/>  
  </H5>

  <xsl:if                         test = "SummarySection"> 
   <UL CLASS="lnone">
    <xsl:for-each               select = "SummarySection">
     <xsl:call-template           name = "TocSummarySection">
      <xsl:with-param             name = "level"
                                select = "1"/>
     </xsl:call-template>  
    </xsl:for-each>
   </UL>
  </xsl:if>

  <hr/>

  <xsl:apply-templates/>
 </xsl:template>


 <!-- 
 =====================================================================
 Display TOC Summary Title with Deletion and Insertion
 ===================================================================== -->
 <xsl:template                   match = "SummaryTitle"
                                  mode = "TOC">
  <xsl:apply-templates/>
 </xsl:template> 


 <!-- 
 =====================================================================
 Display SummarySection in TOC.
 ===================================================================== -->
 <xsl:template                    name = "TocSummarySection">
  <xsl:param                      name = "level"/> 

  <xsl:if                         test = "Title"> 
   <LI>
    <xsl:apply-templates        select = "Title"
                                  mode = "TOC"/>
   </LI>
  </xsl:if>                   

  <xsl:if                         test = "SummarySection and
                                            $level &lt; $showLevel">
   <UL Class="lnone">
    <xsl:for-each               select = "SummarySection">
      <xsl:call-template          name = "TocSummarySection">
       <xsl:with-param            name = "level"
                                select = "$level + 1"/>
      </xsl:call-template>
    </xsl:for-each>
   </UL>
  </xsl:if>
 </xsl:template>


 <!-- 
 ================================================================
 This template deletes data elements not used in the display 
 ================================================================ -->
 <xsl:template                 match = "DateLastModified |
                                        Deletion         |
                                        Diagnosis        |
                                        DocId            | 
                                        Insertion        | 
                                        ItemizedList     |
                                        MainTopics       | 
                                        OrderedList      | 
                                        Para             | 
                                        PatientVersionOf | 
                                        PDQBoard         | 
                                        PdqKey           |
                                        ReplacementFor   |
                                        SecondaryTopics  | 
                                        SectionType      |
                                        SummaryAudience  | 
                                        SummaryLanguage  | 
                                        SummarySection   | 
                                        SummaryTitle     |
                                        SummaryType      |
                                        TranslationOf"/>
</xsl:transform>]]>
</CdrDocXml>
</CdrDoc>