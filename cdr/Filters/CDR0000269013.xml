<CdrDoc Type='Filter' Id='CDR0000269013'>
<CdrDocCtl>
<DocValStatus readonly="yes">U</DocValStatus>
<DocActiveStatus readonly="yes">A</DocActiveStatus>
<DocTitle readonly="yes">Vendor Filter: InScopeProtocol - Sort ProtocolSite</DocTitle>
<ReadyForReview readonly="yes">N</ReadyForReview>
</CdrDocCtl>
<CdrDocXml><![CDATA[<?xml version="1.0"?>

<!-- Creator:  Volker Englisch
     Created:  11/07/2002        
     Comment:  This filter is the first part of a two part filtering
               process.  This part (Vendor Filter: InScopeProtocol -
               Sort ProtocolSite) is sorting ProtocolSite and ProtPerson
               Elements and combining the ProtPerson fragments from
               identical ProtocolSite fragments.
               This second part (Vendor Filter: InScopeProtocol - Remove
               Duplicate ProtocolSite) ensures that each ProtocolSite only
               appears once in the resulting output. -->

<!-- 
 ==================================================================
 $Id: CDR0000269013.xml,v 1.3 2002-11-14 19:12:06 venglisc Exp $

 $Log: not supported by cvs2svn $
 ================================================================== -->
<xsl:transform              xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                            xmlns:cdr = "cips.nci.nih.gov/cdr"
              exclude-result-prefixes = "cdr"
                              version = "1.0">

 <xsl:output                   method = "xml" 
                 omit-xml-declaration = "no"
                             encoding = "UTF-8"
                               indent = "yes"/>


 <!--
 ================================================================
 Default rule, copying back out everything just as we got it.
 ================================================================ -->
 <xsl:template                  match = "@*|comment()|*|
                                         processing-instruction()|text()">
  <xsl:copy>
   <xsl:apply-templates        select = "@*|comment()|*|
                                         processing-instruction()|text()"/>
  </xsl:copy>
 </xsl:template>

 <!--
 ===================================================================
 Template to sort the ProtocolSite elements and only display unique
 entries. 
 =================================================================== -->
 <xsl:template                  match = "ProtocolSites">
    <xsl:copy>
    <!-- This syntax is comparing the text notes only!!! -->
     <xsl:apply-templates      select = "ProtocolSite
                                         [not(.=preceding::ProtocolSite)]">
       <xsl:sort               select = "."/>
     </xsl:apply-templates>

    </xsl:copy>
 </xsl:template>


 <!--  
 ===================================================================
 Template to collect all persons associated with a single site.  In 
 the case of a site being displayed twice this is producing two 
 identical entries.
 Example:
   ORIGINAL          FIRST FILTER         SECOND FILTER
   Site A#F1    \     Site A        \      
     Person a   |       Person a    |     Site A
     Person b   |       Person b    |       Person a
   Site B       |       Person d    |       Person b
     Person c   |==>  Site A        |==>    Person d
   Site A#F2    |       Person a    |     Site B
     Person a   |       Person b    |       Person c
     Person d   /       Person d    |
                      Site B        |
                        Person c    /
 The duplicates are being trimmed in the second filter of this process.
 =================================================================== -->
 <xsl:template                  match = "ProtocolSite">
  <!-- xsl:choose>
   <xsl:when                     test = "contains(@ref, '#')" -->
    <xsl:variable                name = "siteID"   
                               select = "substring-before
                                         (concat(@ref, '#'), '#')"/>
    <!-- xsl:variable                name = "siteIDfrag"   
                               select = "@ref"/ -->
  <ProtocolSite>
   <xsl:attribute                name = "ref">
    <xsl:value-of              select = "$siteID"/>
   </xsl:attribute>

   <xsl:attribute                name = "sitetype">
    <xsl:value-of              select = "./@sitetype"/>
   </xsl:attribute>

   <xsl:copy-of                select = "SiteName"/>

   <xsl:for-each               select = "/Protocol/ProtocolAdminInfo
                                         /ProtocolSites
                                         /ProtocolSite[
                                           substring(@ref, 1, 13)=$siteID]
                                         /ProtPerson[
                                           not(.=preceding::ProtPerson)] |
                                         /Protocol/ProtocolAdminInfo
                                         /ProtocolSites
                                         /ProtocolSite[@ref=$siteID]
                                         /ProtPerson[
                                           not(.=preceding::ProtPerson)]">
    <xsl:sort                  select = "."/>
    <xsl:apply-templates       select = "."/>
   </xsl:for-each>
  </ProtocolSite>
 </xsl:template>

</xsl:transform>
]]>
</CdrDocXml>
</CdrDoc>