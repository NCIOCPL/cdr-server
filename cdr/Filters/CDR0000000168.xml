<CdrDoc Type='Filter' Id='CDR0000000168'>
<CdrDocCtl>
<DocValStatus readonly="yes">U</DocValStatus>
<DocActiveStatus readonly="yes">A</DocActiveStatus>
<DocTitle readonly="yes">Person Protocol Review Report Filter 2</DocTitle>
</CdrDocCtl>
<CdrDocXml><![CDATA[<?xml version="1.0"?>

<xsl:transform               xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                             xmlns:cdr = "cips.nci.nih.gov/cdr"
                               version = "1.0">

 <xsl:import                      href = "cdr:name:Contact Detail Formatter"/>

 <xsl:output                    method = "xml"/>

                                         <!-- default value for testing -->
 <xsl:param                       name = "personLink"
                                select = "'CDR0000039316#F1'"/>

 <!--
 ======================================================================
 Extract elements from a single protocol needed for Person Protocol 
 Review report.
 ====================================================================== -->
 <xsl:template                   match = "/">
  <Protocol>
   <xsl:apply-templates         select = "InScopeProtocol/ProtocolAdminInfo"/>
  </Protocol>
 </xsl:template>

 <!--
 ======================================================================
 Everything we need comes from the protocol admin information.  Find
 the element which contains our person fragment link.
 ====================================================================== -->
 <xsl:template                   match = "ProtocolAdminInfo">
  <xsl:apply-templates          select = "ProtocolLeadOrg/LeadOrgPersonnel
                                          [Person/@cdr:ref = $personLink]"/>
  <xsl:apply-templates          select = "ProtocolLeadOrg/ProtocolSites/
                                          PrivatePracticeSite
                                          [PrivatePracticeSiteID/
                                          @cdr:ref = $personLink]"/>
  <xsl:apply-templates          select = "ProtocolLeadOrg/ProtocolSites/OrgSite/
                                          OrgSiteContact/SpecificPerson
                                          [Person/@cdr:ref = $personLink]"/>
 </xsl:template>

 <!--
 ======================================================================
 Pull out information for a lead org personnel link.
 ====================================================================== -->
 <xsl:template                   match = "LeadOrgPersonnel">
  <LeadOrgPersonnel>
   <xsl:apply-templates         select = "../LeadOrgProtocolID"/>
   <xsl:apply-templates         select = "../LeadOrgRole"/>
   <xsl:apply-templates         select = "../LeadOrgProtocolStatuses
                                            /CurrentOrgStatus/StatusName"/>
   <xsl:apply-templates         select = "PersonRole"/>
   <xsl:apply-templates         select = "ProtocolSpecificContact"/>
  </LeadOrgPersonnel>
 </xsl:template>

 <!--
 ======================================================================
 Pull out information for a private practice link.
 ====================================================================== -->
 <xsl:template                   match = "PrivatePracticeSite">
  <PrivatePracticeSite>
   <xsl:apply-templates         select = "../../LeadOrgProtocolID"/>
   <xsl:apply-templates         select = "../../LeadOrgRole"/>
   <xsl:apply-templates         select = "../../LeadOrgProtocolStatuses
                                               /CurrentOrgStatus/StatusName"/>
   <xsl:apply-templates         select = "Role"/>
  </PrivatePracticeSite>
 </xsl:template>

 <!--
 ======================================================================
 Pull out information for a specific person link.
 ====================================================================== -->
 <xsl:template                   match = "SpecificPerson">
  <SpecificPerson>
   <xsl:apply-templates         select = "../../../../LeadOrgProtocolID"/>
   <xsl:apply-templates         select = "../../../../LeadOrgRole"/>
   <xsl:apply-templates         select = "../../../../LeadOrgProtocolStatuses
                                                     /CurrentOrgStatus
                                                     /StatusName"/>
   <xsl:apply-templates         select = "Role"/>
   <xsl:apply-templates         select = "SpecificPhone"
                                  mode = "only-specific-phone"/>
  </SpecificPerson>
 </xsl:template>

 <!--
 ======================================================================
 Pull out the value for a lead organization's ID for the protocol.
 ====================================================================== -->
 <xsl:template                   match = "LeadOrgProtocolID">
  <Id>
   <xsl:value-of                select = "."/>
  </Id>
 </xsl:template>

 <!--
 ======================================================================
 Pull out the value for the lead organization's role for the protocol.
 ====================================================================== -->
 <xsl:template                   match = "LeadOrgRole">
  <OrgRole>
   <xsl:value-of                select = "."/>
  </OrgRole>
 </xsl:template>

 <!--
 ======================================================================
 Pull out the value for a lead organization's status for the protocol.
 ====================================================================== -->
 <xsl:template                   match = "StatusName">
  <Status>
   <xsl:value-of                select = "."/>
  </Status>
 </xsl:template>

 <!--
 ======================================================================
 Pull out the value for the person's role for the protocol.
 ====================================================================== -->
 <xsl:template                   match = "Role|PersonRole">
  <PersonRole>
   <xsl:value-of                select = "."/>
  </PersonRole>
 </xsl:template>

 <!--
 ======================================================================
 Grab the override for the person's phone number.
 ====================================================================== -->
 <xsl:template                   match = "SpecificPhone"
                                  mode = "only-specific-phone">
  <SpecificContact>
   <Line>
    <xsl:value-of               select = "concat('Phone: ', .)"/>
   </Line>
  </SpecificContact>
 </xsl:template>

 <!--
 ======================================================================
 Grab the override for the person's specific contact.
 ====================================================================== -->
 <xsl:template                   match = "ProtocolSpecificContact">
  <SpecificContact>
   <xsl:apply-templates         select = "PostalAddress"/>
   <xsl:apply-templates         select = "Phone"/>
   <xsl:apply-templates         select = "TollFreePhone"/>
   <xsl:apply-templates         select = "Fax"/>
   <xsl:apply-templates         select = "Email"/>
   <xsl:apply-templates         select = "WebSite"/>
  </SpecificContact>
 </xsl:template>

</xsl:transform>
]]>
</CdrDocXml>
</CdrDoc>