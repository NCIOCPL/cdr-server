<CdrDoc Type='Filter' Id='CDR0000000133'>
<CdrDocCtl>
<DocValStatus readonly="yes">U</DocValStatus>
<DocActiveStatus readonly="yes">A</DocActiveStatus>
<DocTitle readonly="yes">Person at Org Location</DocTitle>
<ReadyForReview readonly="yes">N</ReadyForReview>
</CdrDocCtl>
<CdrDocXml><![CDATA[<?xml version="1.0"?>
<xsl:transform             xmlns:xsl = "http://www.w3.org/1999/XSL/Transform"
                           xmlns:cdr = "cips.nci.nih.gov/cdr"
                             version = "1.0">

 <xsl:param                     name = "fragLink"/>
 <xsl:output                  method = "html"/>

 <!-- ===================================================================
      We want name information, and any specific overriding contact 
      information.
      =================================================================== -->
 <xsl:template match="/">
  <table                      border = "0"
                         cellpadding = "0"
                         cellspacing = "0">
   <xsl:apply-templates       select = "Person/PersonNameInformation"/>
   <xsl:apply-templates       select = "Person/PersonLocations/
                                        OtherPracticeLocation
                                        [OrganizationLocation/
                                        @cdr:ref=$fragLink]"/>
  </table>
 </xsl:template>

 <!-- ===================================================================
      Put together a row for the name.
      =================================================================== -->
 <xsl:template                 match = "PersonNameInformation">
  <tr>
   <td                       colspan = "2">
    <font                       size = "3">
     <!-- Added per S. Khanna, request #821 2003-08-22. -->
     <xsl:if                    test = "../Status/CurrentStatus='Inactive'">
      <xsl:text>Inactive;</xsl:text>
     </xsl:if>

     <xsl:if                    test = "GivenName">
      <xsl:value-of           select = "GivenName"/>
      <xsl:text> </xsl:text>
     </xsl:if>
     <xsl:value-of            select = "SurName"/>
     <xsl:for-each            select = "ProfessionalSuffix/
                                        StandardProfessionalSuffix|
                                        ProfessionalSuffix/
                                        CustomProfessionalSuffix">
      <xsl:text>, </xsl:text>
      <xsl:value-of           select = "."/>
     </xsl:for-each>
    </font>
   </td>
  </tr>
 </xsl:template>

 <!-- ===================================================================
      Extract the address (as HTML).
      =================================================================== -->
 <xsl:template                 match = "OtherPracticeLocation">
  <xsl:apply-templates        select = "PersonTitle"/>
  <xsl:apply-templates        select = "SpecificPostalAddress"/>
  <xsl:apply-templates        select = "SpecificPhone"/>
  <xsl:apply-templates        select = "SpecificTollFreePhone"/>
  <xsl:apply-templates        select = "SpecificFax"/>
  <xsl:apply-templates        select = "SpecificEmail"/>
  <xsl:apply-templates        select = "SpecificWebSite"/>
 </xsl:template>

 <!-- ===================================================================
      Generic template for wrapping a line as an HTML table row.
      =================================================================== -->
 <xsl:template                 match = "PersonTitle|Street">
  <tr>
   <td                       colspan = "2">
    <font                       size = "3">
     <xsl:value-of            select = "."/>
    </font>
   </td>
  </tr>
 </xsl:template>

 <!-- ===================================================================
      Assemble the postal address as HTML.
      =================================================================== -->
 <xsl:template                 match = "SpecificPostalAddress">
  <xsl:apply-templates        select = "Street"/>
  <xsl:variable                 name = "city">
   <xsl:if                      test = "City">
    <xsl:value-of             select = "City"/>
    <xsl:if                     test = "CitySuffix">
     <xsl:text> </xsl:text>
     <xsl:value-of            select = "CitySuffix"/>
    </xsl:if>
   </xsl:if>
  </xsl:variable>
  <xsl:variable                 name = "state">
   <xsl:if                      test = "PoliticalSubUnit_State[@cdr:ref]">
    <xsl:variable               name = "stateDoc"
                              select = "document(concat('cdrx:',
                                                 PoliticalSubUnit_State/
                                                 @cdr:ref))"/>
    <xsl:choose>
     <xsl:when                  test = "$stateDoc/PoliticalSubUnit/
                                        PoliticalSubUnitShortName">
      <xsl:value-of           select = "$stateDoc/PoliticalSubUnit/
                                        PoliticalSubUnitShortName"/>
     </xsl:when>
     <xsl:when                  test = "$stateDoc/PoliticalSubUnit/
                                        PoliticalSubUnitFullName">
      <xsl:value-of           select = "$stateDoc/PoliticalSubUnit/
                                        PoliticalSubUnitFullName"/>
     </xsl:when>
    </xsl:choose>
   </xsl:if>
  </xsl:variable>
  <xsl:variable                 name = "cityLine">
   <xsl:value-of              select = "$city"/>
   <xsl:if                      test = "$city and $state">
    <xsl:text>, </xsl:text>
   </xsl:if>
   <xsl:value-of              select = "$state"/>
   <xsl:if                      test = "$state and PostalCode_ZIP">
    <xsl:text> </xsl:text>
   </xsl:if>
   <xsl:value-of              select = "PostalCode_ZIP"/>
  </xsl:variable>
  <xsl:if                       test = "$cityLine">
   <tr>
    <td                      colspan = "2">
     <font                      size = "3">
      <xsl:value-of           select = "$cityLine"/>
     </font>
    </td>
   </tr>
  </xsl:if>
  <xsl:if                       test = "Country[@cdr:ref]">
   <xsl:variable                name = "country"
                              select = "document(concat('cdrx:',
                                                 Country/@cdr:ref))"/>
   <xsl:if                      test = "$country/Country/CountryFullName">
    <tr>
     <td                     colspan = "2">
      <font                     size = "3">
       <xsl:value-of          select = "$country/Country/CountryFullName"/>
      </font>
     </td>
    </tr>
   </xsl:if>
  </xsl:if>
 </xsl:template>

 <!-- ===================================================================
      Assemble the miscellaneous data elements as HTML.
      =================================================================== -->
 <xsl:template                 match = "SpecificPhone|
                                        SpecificTollFreePhone|
                                        SpecificFax|
                                        SpecificEmail|
                                        SpecificWebSite">
  <tr>
   <td>
    <font                      size = "3">
     <xsl:value-of           select = "substring-after(name(), 'Specific')"/>
     <xsl:text>:&#160;&#160;</xsl:text>
    </font>
   </td>
   <td>
    <font                      size = "3">
     <xsl:choose>
      <xsl:when                test = "name() = 'SpecificWebSite'">
       <xsl:value-of         select = "@cdr:xref"/>
      </xsl:when>
      <xsl:otherwise>
       <xsl:value-of         select = "."/>
      </xsl:otherwise>
     </xsl:choose>
    </font>
   </td>
  </tr>
 </xsl:template>

</xsl:transform>
]]>
</CdrDocXml>
</CdrDoc>